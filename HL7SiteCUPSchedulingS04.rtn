^MAC^Save for Source Control^^~Format=IRIS.S~^RAW
%RO
HL7SiteCUPSchedulingS04^MAC^^^0
HL7SiteCUPSchedulingS04 // ML 19/10/2005 ; HL7 interface : scheduling - CUP 2000
SrcVer ;; $Id: //trak/main/releases/T2020/1/CLXX/rtn/hl7/HL7SiteCUPSchedulingS04.rtn#1 $
	quit
CheckS04 ;; S04 - request appointment cancellation
S04()
	set (UBRN,USRN,EBEInum,cancelreason,reject)=""
	set cnt1=0 for  set cnt1=$order(^TMP("HL7",$job,cnt1)) quit:cnt1=""  do  quit:reject'=""
	.if ",MSH,MSA,EVN,QRD,QRF,QAK,"[(","_cnt1_",") quit
	.if '$data(^TMP("HL7",$job,cnt1,"ARQ")) set reject="No ARQ segment contained within message" quit
	. // extract unique booking number and EBEI number
 	.set UBRN=$get(^TMP("HL7",$job,cnt1,"ARQ",1)),EBEInum=$get(^TMP("HL7",$job,cnt1,"ARQ",2)),xEBEInum=EBEInum
 	.if UBRN'="" set UBRN=$$TextConversion^HL7Common12(UBRN,"I")
 	.if EBEInum'="" set EBEInum=$$TextConversion^HL7Common12(EBEInum,"I")
 	.if EBEInum="" set reject="EBEI number is required to cancel an appointment and was not received" quit
	.set cancelreason=$piece($get(^TMP("HL7",$job,cnt1,"ARQ",6)),"^",1)
 	. // supress any outbound 'SIU' messages
	.set HL7Flag="Y"
	. // cancel required appointments
	.set ok=##class(web.RBAppointment).CancelAppt($get(HL7("USER")),"",UBRN,cancelreason,"",EBEInum)
	.if 'ok do  quit
	..set reject="Unable to cancel appointment/s for EBEI "_xEBEInum_"."
	.kill HL7Flag
	if reject'="" do Reject^HL7Reject(reject,"R")
	// build SRR response
	set ^TMP("HL7-OUT",$job,"MSA",1)=$select(reject="":"AA",1:"AR")
	set ^TMP("HL7-OUT",$job,"MSA",2)=$get(^TMP("HL7",$job,"MSH",9))
	set ^TMP("HL7-OUT",$job,"MSA",3)=reject
	do Build^HL7Queue("MSA",12)
	if reject'="" do
	.set ^TMP("HL7-OUT",$job,"ERR",1)=$get(reject)
	.do Build^HL7Queue("ERR",1)
	if reject="" do
	.set ^TMP("HL7-OUT",$job,"SCH",1)=$$TextConversion^HL7Common12(UBRN,"O"),^TMP("HL7-OUT",$job,"SCH",2)=xEBEInum
	.do Build^HL7Queue("SCH",$list(##class(websys.StandardTypeItem).GetDescriptionPlus("HL7Segment","SCH"),2))
	// send response
	if $data(queue) do  kill queue
	.set controlid=$$ControlId^HL7AckNak
	. // reverse sending and receiving applications found in interface info
	.set mshmsg="MSH|^~\&|"_$get(HL7("ACKSENDAPP"))_"|"_$get(HL7("ACKSENDFAC"))_"|"_$get(^TMP("HL7",$job,"MSH",2))_"|"_$get(^TMP("HL7",$job,"MSH",3))_"|"_$$Date^HL7_"||SRR^S04|"_$$ControlId^HL7AckNak_"|"_$get(HL7("PT"))_"|"_$get(HL7("VER"))_"|||AL|NE||||EN|"
	.kill trace set trace=1
	.write ESC("VT"),mshmsg,ESC("CR") set trace(trace)=mshmsg
	.set cnt="" for  set cnt=$order(queue(cnt)) quit:cnt=""  do
	..write queue(cnt),ESC("CR") set trace=$get(trace)+1,trace(trace)=queue(cnt)
	.write ESC("FS"),ESC("CR"),!
	.do trace^HL7Trace("M-->E",$h,"",tracerowid) kill trace
	set reject=""
	quit ""



