^MAC^Save for Source Control^^~Format=IRIS.S~^UTF8
%RO
HL7Create7^MAC^^^0
HL7Create7 // ML 09/05/2006 ; HL7 interface : create transmission packets
SrcVer ;; $Id: //trak/main/releases/T2020/1/CLXX/rtn/hl7/HL7Create7.rtn#1 $
	quit
	
CheckPRB ;; build DATA array - PRB
PRB()	set intClinPath=$get(intClinPath),intAdmNum=$get(intAdmNum),intMRAdmNum=$piece(intClinPath,"||") kill DATA
	
	// extract clinical pathways detail
	kill ClinPathWay if $$ClinPathWay^HL7OutExtract13(intClinPath,.ClinPathWay) quit 0
	
	// extract pathways detail
	kill PathWay if $$PathWay^HL7OutExtract13($piece(ClinPathWay(3),"||"),.PathWay) quit 0
	
	// extract admission number
	set AdmNum="" if intAdmNum'="" set AdmNum=$$extAdmNum^HL7Common2(intAdmNum)
	
	set code=$get(PathWay(2)),desc=$get(PathWay(3)),ClinPathType=$get(PathWay(7))
	kill datetime for i="13^^ADDED^Y","15^^REVIEW^Y","16^^END^Y","19^^START^Y" do
	. if $get(ClinPathWay($piece(i,"^")))'="" set datetime($piece(i,"^",3))=$$DateTime^HL7Common2($get(ClinPathWay($piece(i,"^"))),"",$piece(i,"^",4))
	
	set DATA("PRB",2)=$get(datetime("ADDED")),DATA("PRB",3)=code_"^"_desc,DATA("PRB",5)=AdmNum,DATA("PRB",7)=$get(datetime("START"))
	set DATA("PRB",8)=$get(datetime("REVIEW")),DATA("PRB",9)=$get(datetime("END")),DATA("PRB",10)=ClinPathType,DATA("PRB",16)=$extract($get(datetime("START")),1,8)
	
	kill ClinPathWay,PathWay,datetime
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
	quit 0
	
CheckGOL ;; build DATA array - GOL
GOL()	set intClinPath=$get(intClinPath),intAdmNum=$get(intAdmNum),intMRAdmNum=$piece(intClinPath,"||") kill DATA
	
	// extract clinical pathways detail
	kill ClinPathWay if $$ClinPathWay^HL7OutExtract13(intClinPath,.ClinPathWay) quit 0
	
	// extract goal
	kill PathDays if $$PathDays^HL7OutExtract13(ClinPathWay(3),.PathDays) quit 0
	
	// extract admission number
	set AdmNum="" if intAdmNum'="" set AdmNum=$$extAdmNum^HL7Common2(intAdmNum)
	
	set goal="^"_$get(PathDays(5))_"^TRAKCARE^^^"
	kill datetime for i="18^^TARGET^Y","19^^START^Y" do
	. if $get(ClinPathWay($piece(i,"^")))'="" set datetime($piece(i,"^",3))=$$DateTime^HL7Common2($get(ClinPathWay($piece(i,"^"))),"",$piece(i,"^",4))
	
	set DATA("GOL",2)=$get(datetime("START")),DATA("GOL",3)=goal,DATA("GOL",5)=AdmNum,DATA("GOL",8)=$get(datetime("TARGET"))
	
	kill ClinPathWay,PathDays,datetime
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
	quit 0
	
CheckPTH ;; build DATA array - PTH
PTH()	set intClinPath=$get(intClinPath),intAdmNum=$get(intAdmNum),intMRAdmNum=$piece(intClinPath,"||") kill DATA
	
	// extract clinical pathways detail
	kill ClinPathWay if $$ClinPathWay^HL7OutExtract13(intClinPath,.ClinPathWay) quit 0
	
	// extract admission number
	set AdmNum="" if intAdmNum'="" set AdmNum=$$extAdmNum^HL7Common2(intAdmNum)
	
	set DATA("PTH",2)=AdmNum
	
	kill ClinPathWay
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
	quit 0
	
CheckRF1 ;; build DATA array - RF1
RF1()	set intDSNum=$get(intDSNum),intRefNum=$get(intRefNum) kill DATA
	
	if msgtype="REF^I12" do
	. // extract discharge summary detail
	. kill DischSum if $$DischSum^HL7OutExtract14(intDSNum,.DischSum) quit
	. set intRefNum=$get(DischSum(66))
	. // extract referral detail
	. if intRefNum'="" do
	. . kill Referral if $$Referral^HL7OutExtract14(intRefNum,.Referral)
	
	. // extract code table detail
	. set mtable="PA_DischargeSummary" do CodeTable^HL7OutExtract14(mtable)
	. set disposition=$get(DischSum(45)),priority=$get(DischSum(46)),category=$get(DischSum(47)),reason=$get(DischSum(48)),status=$get(DischSum(52)),ReferralId=$get(DischSum(53))
	. for j=49,50,51 set date($select(j=49:"EFF",j=50:"EXP",1:"PRO"))=$select($get(DischSum(j))'="":$$Date^HL7($get(DischSum(j))),1:"")
	
	. // build referral numbers
	. for type="CSINT","CSNUM","GRP","REF" if $data(RefNum(type)) do
	. . set number=$select(type="CSINT":$get(DischSum(1)),type="CSNUM":$get(DischSum(53)),type="REF":$get(Referral(19)),1:$get(Referral(49)))
	. . if number="" quit
	. . set segtype=$piece(RefNum(type),"^"),field=$piece(RefNum(type),"^",2),assigntype=$piece(RefNum(type),"^",3),assigncode=$piece(RefNum(type),"^",4),idcode=$piece(RefNum(type),"^",5)
	. . if assigntype'="" set $piece(number,"^",assigntype)=assigncode
	. . set $piece(number,"^",5)=idcode
	. . set DATA(segtype,field)=$get(DATA(segtype,field))_$select($length($get(DATA(segtype,field))):"~",1:"")_number
	
	. set DATA("RF1",1)=status_"^TRAKCARE",DATA("RF1",2)=priority_"^TRAKCARE",DATA("RF1",4)=disposition_"^TRAKCARE",DATA("RF1",5)=category_"^TRAKCARE"
	. set DATA("RF1",7)=date("EFF"),DATA("RF1",8)=date("EXP"),DATA("RF1",9)=date("PRO"),DATA("RF1",10)=reason_"^TRAKCARE"
	
	if msgtype="REF^I13" do
	. // extract referral detail
	. kill Referral if $$Referral^HL7OutExtract14(intRefNum,.Referral) quit
	. // extract code table detail
	. set mtable="RB_Referral" do CodeTable^HL7OutExtract14(mtable)
.
	. set status="" if $get(Referral(17))'="" set status=$get(Referral(17))_"^"_##class(websys.StandardTypeItem).GetDescriptionFromCode("RBReferralStatus",$get(Referral(17)))
	. set priority=$get(Referral(38))
	
	. // build referral numbers
	. for type="GRP","REF" if $data(RefNum(type)) do
	. . set number=$select(type="REF":$get(Referral(19)),1:$get(Referral(49)))
	. . if number="" quit
	. . set segtype=$piece(RefNum(type),"^"),field=$piece(RefNum(type),"^",2),assigntype=$piece(RefNum(type),"^",3),assigncode=$piece(RefNum(type),"^",4),idcode=$piece(RefNum(type),"^",5)
	. . if assigntype'="" set $piece(number,"^",assigntype)=assigncode
	. . set $piece(number,"^",5)=idcode
	. . set DATA(segtype,field)=$get(DATA(segtype,field))_$select($length($get(DATA(segtype,field))):"~",1:"")_number
	
	. set DATA("RF1",1)=status_"^TRAKCARE",DATA("RF1",2)=priority_"^TRAKCARE"
	
	kill DischSum,Referral,doctor
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
	
	quit 0
	
CheckPRD ;; build DATA array - PRD
PRD()	set intDSNum=$get(intDSNum),(role,location,PrefContact,count)="" kill DATA,CareProv
.
	if msgtype="REF^I12" do
	. // extract discharge summary detail
	. kill DischSum if $$DischSum^HL7OutExtract14(intDSNum,.DischSum) quit
	
	. // discharging care provider
	. if $get(DischSum(19))'="" do
	. . kill CareProv if $$CareProvider^HL7OutExtract10($get(DischSum(19)),.CareProv) quit
	. . set role="RP^^TRAKCARE",location=""
	. . do ProvLoc("DOC-C")
	. // extract 'discharged/referred to' providers
	. set row="0" for  set row=$order(^PA("DISSUM",intDSNum,"REF",row)) quit:row=""  do
	. . set drid=$piece(^PA("DISSUM",intDSNum,"REF",row),"^") quit:drid=""
	. . kill RefDoctor if $$RefDoctor^HL7OutExtract10(drid,.RefDoctor) quit
	. . set role="RT^^TRAKCARE",location=""
	. . do ProvLoc("DOC-R")
	. // extract discharge care providers
	. set row="0" for  set row=$order(^PA("DISSUM",intDSNum,"DOC",row)) quit:row=""  do
	. . set drid=$piece(^PA("DISSUM",intDSNum,"DOC",row),"^") quit:drid=""
	. . kill CareProv if $$CareProvider^HL7OutExtract10(drid,.CareProv) quit
	. . set role="RT^^TRAKCARE",location=""
	. . do ProvLoc("DOC-C")
	. // extract discharge location
	. set row="0" for  set row=$order(^PA("DISSUM",intDSNum,"LOC",row)) quit:row=""  do
	. . set intLoc=$piece(^PA("DISSUM",intDSNum,"LOC",row),"^") quit:intLoc=""
	. . kill Location if $$Location^HL7OutExtract15(intLoc,.Location) quit
	. . set location=$get(Location(3)),phoneW=$get(Location(67))_$char(1)_$get(Location(68)),pager=$get(Location(82)),email=$get(Location(83)),fax=$get(Location(84))
	. . set role="RT^^TRAKCARE"
	. . do ProvLoc
	. // extract discharge non-government organisation
	. set row="0" for  set row=$order(^PA("DISSUM",intDSNum,"NG",row)) quit:row=""  do
	. . set intNGO=$piece(^PA("DISSUM",intDSNum,"NG",row),"^") quit:intNGO=""
	. . kill NonGovOrg if $$NonGovOrg^HL7OutExtract15(intNGO,.NonGovOrg) quit
	. . set mtable="PAC_NonGovOrg" do CodeTable^HL7OutExtract15(mtable)
	. . set location=$get(NonGovOrg(3)),phoneW=$get(NonGovOrg(8)),fax=$get(NonGovOrg(9)),email=$get(NonGovOrg(10)),PrefContact=$get(NonGovOrg(11)),role="RT^^TRAKCARE"
	. . do ProvLoc
	
	if msgtype="REF^I13" do
	. // extract referral detail
	. kill Referral if $$Referral^HL7OutExtract14(intRefNum,.Referral) quit
	. // referring doctor
	. if $get(Referral(23))'="" do
	. . kill RefDoctor if $$RefDoctor^HL7OutExtract10($get(Referral(23)),.RefDoctor) quit
	. . set role="RP^^TRAKCARE",location=""
	. . do ProvLoc("DOC-R")
.
	kill DischSum,Referral,CareProv,NonGovOrg,Location
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
.
	quit 0
.
	// set provider details
ProvLoc(prdtype) set prdtype=$get(prdtype),count=count+1
	set (code,name,nameD,nameL,nameG,nameO,nameT)=""
.
	// build doctor details
	if prdtype["DOC" do
	. set (phoneH,phoneW,phoneM,fax,email,pager,PrefContact)=""
	. // care provider
	. if prdtype="DOC-C" do
	. . set mtable="CT_CareProv" do CodeTable^HL7OutExtract10(mtable)
	. . set code=$get(CareProv(2)),nameD=$get(CareProv(3)),nameL=$get(CareProv(75)),nameG=$get(CareProv(72)),nameO=$get(CareProv(73)),nameT=$get(CareProv(49))
	. . set phoneW=$get(CareProv(18))_$char(1)_$get(CareProv(19)),phoneH=$get(CareProv(20)),pager=$get(CareProv(21)),phoneM=$get(CareProv(51)),fax=$get(CareProv(52))
	. . set email=$get(CareProv(54)),PrefContact=$get(CareProv(55))
	. // referring doctor
	. if prdtype="DOC-R" do
	. . set mtable="PAC_RefDoctor" do CodeTable^HL7OutExtract10(mtable)
	. . set code=$get(RefDoctor(2)),(nameD,nameL)=$get(RefDoctor(3)),nameG=$get(RefDoctor(51)),nameO=$get(RefDoctor(54)),nameT=$get(RefDoctor(4))
	. . set phoneH=$get(RefDoctor(10)),phoneW=$get(RefDoctor(42)),fax=$get(RefDoctor(43)),phoneM=$get(RefDoctor(44)),email=$get(RefDoctor(45)),PrefContact=$get(RefDoctor(46)),pager=$get(RefDoctor(52))
	. set name=nameD_"^^^^" if $length(nameL),$length(nameG) set name=nameL_"^"_nameG_"^"_nameO_"^^"_nameT
	. set code=code_"^GpCode",location=""
.
	// set contact details
	set contact="" for var="phoneH","phoneW","phoneM","email","fax","pager" if $get(@var)'="" do
	. if var="phoneH" set contact=contact_$select(contact'="":"~",1:"")_@var_"^PRN^PH^^^^^^"
	. if var="phoneW" set contact=contact_$select(contact'="":"~",1:"")_$piece(@var,$char(1))_"^WPN^PH^^^^^"_$piece(@var,$char(1),2)_"^"
	. if var="phoneM" set contact=contact_$select(contact'="":"~",1:"")_@var_"^ORN^CP^^^^^^"
	. if var="email" set contact=contact_$select(contact'="":"~",1:"")_"^NET^Internet^"_@var_"^^^^^"
	. if var="fax" set contact=contact_$select(contact'="":"~",1:"")_@var_"^ORN^FX^^^^^^"
	. if var="pager" set contact=contact_$select(contact'="":"~",1:"")_@var_"^BPN^BP^^^^^^"
.
	if count=1 do  quit
	. set DATA("PRD",1)=role,DATA("PRD",2)=name,DATA("PRD",4)=$select(location'="":"^^^^^^^^"_location,1:""),DATA("PRD",5)=contact,DATA("PRD",6)=PrefContact,DATA("PRD",7)=code
	
	if count>1,repeat="Y" do
	. set Repeat("PRD",count,1)=role,Repeat("PRD",count,2)=name,Repeat("PRD",count,4)=$select(location'="":"^^^^^^^^"_location,1:""),Repeat("PRD",count,5)=contact,Repeat("PRD",count,6)=PrefContact,Repeat("PRD",count,7)=code
.
	set (role,location,phoneH,phoneW,phoneM,fax,email,pager,PrefContact)=""
	quit



