^MAC^Save for Source Control^^~Format=IRIS.S~^RAW
%RO
HL7SiteQPCreate1^MAC^^^0
HL7SiteQPCreate1 // ML 18/9/01 ; HL7 interface : create transmission packets - Qatar Petroleum
SrcVer ;; $Id: //trak/main/releases/T2020/1/CLXX/rtn/hl7/HL7SiteQPCreate1.rtn#1 $
	quit
 
CheckPID ;; build data array - PID
PID()	set intRegMrn=$get(intRegMrn),intAdmNum=$get(intAdmNum) kill DATA
 
	if msgtype["A37",$length($get(intRegCheck)) do
	.if intRegCheck'=intRegTo set intRegMrn=intRegTo
 
	// extract patient details
	kill PatDetail,PatDetailx set SQLCODE=$$PatDetail^HL7OutExtract1(intRegMrn,.PatDetail,.PatDetailx)
	if SQLCODE quit SQLCODE
 
	// check for new null fields and change to ""
	for xtable="PAPER","PAPMI" if $data(old(xtable)) do  quit
	.do NullChange^HL7Common3(xtable)
 
	// extract patient address details
	do BuildAddress^HL7OutExtract1
 
	// extract code table detail
	for mtable="PA_Person","PA_PatMas" do CodeTable^HL7OutExtract1A(mtable)
 
	// extract patient number detail
	if msgtype'["A37" do  if SQLCODE quit SQLCODE
	.kill number set SQLCODE=$$PatNum^HL7OutExtract1(intRegMrn,intAdmNum,.number)
 
	if msgtype["A37" do  if SQLCODE quit SQLCODE
	.kill number set SQLCODE=$$PatNum^HL7OutExtract1(intRegMrn,intAdmNum,.number)
	. // extract numbers required for first PID
	.if intRegMrn'=intRegTo do  quit
	..set intMergeNum=$order(^PAMRi("PAT-FROM",intRegTo,"")) if intMergeNum'="" do
	...set row="" for  set row=$order(^PAMR(intMergeNum,"DET",row)) quit:row=""  if $piece(^PAMR(intMergeNum,"DET",row),"^")="RTMAS" do
	....set intHospFrom=$piece($piece(^PAMR(intMergeNum,"DET",row),"^",2),":") if intHospFrom="" quit
	....kill number("HOSP",intHospFrom)
	. // extract numbers required for second PID/mrn's moved
	.set intMergeNum=$order(^PAMRi("PAT-FROM",intRegMrn,"")) if intMergeNum'="" do
	..set row="" for  set row=$order(^PAMR(intMergeNum,"DET",row)) quit:row=""  if $piece(^PAMR(intMergeNum,"DET",row),"^")="RTMAS" do
	...set intHospFrom=$piece($piece(^PAMR(intMergeNum,"DET",row),"^",2),":") if intHospFrom="" quit
	...set active=$piece($piece(^PAMR(intMergeNum,"DET",row),"^",2),":",2)
	...if '$data(^RT(intHospFrom)) quit
	...do NumTypeDetail^HL7Common2(PatNum("HOSP"),"","",.asstype,.asscode,.idcode,.idmrtype)
	...do MedRecord^HL7OutExtract1(intHospFrom)
	...set num=$piece(^RT(intHospFrom),"^"),$piece(num,"^",asstype)=code,$piece(num,"^",5)=idcode,$piece(num,"^",6)=$piece($piece(num,"^",6),"&")_"&"_$select(active="Y":"A",active="D":"D",1:"I")
	...set number("HOSP")=$get(number("HOSP"))_$select($length($get(number("HOSP"))):"~",1:"")_num
 
	// extract patient alias
	kill alias set Alias="" for altype="ALIAS","SUR" do 
	.set row=0 for  set row=$order(^PAPER(intRegMrn,altype,row)) quit:row=""  do
	..set intAliasNum=intRegMrn_"||"_row
	..set al=$$PatAlias^HL7OutExtract1(intAliasNum,altype) if $translate($piece(al,"|"),",")="",($translate($piece(al,"|"),"_")="") quit
	..set sep="," if $length($piece(al,"|"),"_")=7 set sep="_"
	..set active=$piece(al,"|",2),al=$piece(al,"|",1),$piece(al,sep,7)="A&"_$select(active="Y":"A",1:"I"),alias(al)=""
	set al="" for  set al=$order(alias(al)) quit:al=""  do
	.set sep="," if $length($piece(al,"|"),"_")=7 set sep="_"
	.set Alias=Alias_$select($length(Alias):"~",1:"")_$translate(al,sep,"^")
 
	set sex=$get(PatDetail(15)),marital=$get(PatDetail(20)),religion=$get(PatDetail(23)),phoneW=$get(PatDetail(36)),phoneH=$get(PatDetail(38))
	set country=$get(PatDetail(77)),email=$get(PatDetail(138)),title=$get(PatDetail(139)),phoneM=$get(PatDetail(140)),indigenous=$get(PatDetail(141))
	set language=$get(PatDetail(143))
	set MotherNum="" if $get(PatDetail(128))'="" set MotherNum=$$extRegMrn^HL7Common2(PatDetail(128))
 
	// check dates for null values
	set dob=$get(PatDetail(11)) if dob=""""!(dob="""""") set estdob=""""""
	if dob'="",dob'="""""" do
	.set dob=$$DateConv^HL7Common2(.reject,"IE",PatDetail(11),"B"),estdob=$get(PatDetail(152))
	set death=$get(PatDetail(74)) if death'="",death'="""""" do
	.set death=$$Date^HL7(PatDetail(74)_","_PatDetail(82))
 
	// adjust contact numbers
	set (Phone13,Phone14)=""
	if HL7("VER")'="2.3.1",HL7("VER")'="2.4" do
	.if phoneH'=""!(email'="") set phoneH=phoneH_"^^^"_email
	.set Phone13=phoneH,Phone14=phoneW
	if HL7("VER")="2.3.1" do
	.set phoneH=$select(phoneH'="":phoneH_"^PRN^PH^^^^^^",1:""),phoneM=$select(phoneM'="":phoneM_"^ORN^CP^^^^^^",1:""),phoneW=$select(phoneW'="":phoneW_"^WPN^PH^^^^^^",1:""),email=$select(email'="":"^NET^Internet^"_email_"^^^^^",1:"")
	.for var="phoneH","phoneM","email" if $get(@var)'="" set Phone13=Phone13_$select($length(Phone13):"~",1:"")_$get(@var)
	.set Phone14=$get(phoneW)
	if HL7("VER")="2.4" do
	.set phoneH=$select(phoneH'="":phoneH_"^PRN^PH^^^^^^P",1:""),phoneM=$select(phoneM'="":phoneM_"^ORN^CP^^^^^^P",1:""),phoneW=$select(phoneW'="":phoneW_"^WPN^PH^^^^^^P",1:""),email=$select(email'="":"^NET^Internet^"_email_"^^^^^",1:"")
	.for var="phoneH","phoneM","email" if $get(@var)'="" set Phone13=Phone13_$select($length(Phone13):"~",1:"")_$get(@var)
	.set Phone14=$get(phoneW)
	.set phtype="" for  set phtype=$order(phone(phtype)) quit:phtype=""  do
	..set PhoneType=$piece($$Address^HL7Common7(.reject,"O","TYP",phtype),"|")
	..set phcnt="" for  set phcnt=$order(phone(phtype,phcnt)) quit:phcnt=""  do
	...set Phone13=Phone13_$select($length(Phone13):"~",1:"")_$select(phone(phtype,phcnt)'="":phone(phtype,phcnt)_"^"_"PRN"_"^"_"PH"_"^^^^^^"_PhoneType,1:"")
 
	// adjust name
	set name=$$BuildName^HL7Common3($get(PatDetail(4)),$get(PatDetail(188)),$get(PatDetail(118)))
	set $piece(name,"^",5)=$piece(title,"^")_"^^L"
 
	// setup permanent/temp address
	set (Address("PERM"),Address("TEMP"))=""
	if $data(PermAddress) do
	.for k="CTY","PRV","PST" set PermAddress(k)=$piece($$Address^HL7Common7(.reject,"O",k,$get(PermAddress(k))),"|",$select(k="CTY":2,1:1))
	. // check if permanent address has details
	.set (line,linefound)="" for  set line=$order(PermAddress(line)) quit:line=""  if $get(PermAddress(line))'="" set linefound=1
	.if 'linefound quit
	.set Address("PERM")=$get(PermAddress("LINE1"))_"^"_$get(PermAddress("LINE2"))_"^"_$get(PermAddress("CTY"))_"^"_$get(PermAddress("PRV"))_"^"_$get(PermAddress("PST"))_"^^P^"_$get(PermAddress("ADDR2"))_"^^^"
	if $data(TempAddress) do
	.set cnt="" for  set cnt=$order(TempAddress(cnt)) quit:cnt=""  do
	..for k="CTY","PRV","PST","TYP","FOR-COU" set TempAddress(cnt,k)=$piece($$Address^HL7Common7(.reject,"O",$select(k="FOR-COU":"COU",1:k),$get(TempAddress(cnt,k))),"|",$select(k="CTY":2,1:1))
	.. // check if temporary address has details
	..set (line,linefound)="" for  set line=$order(TempAddress(cnt,line)) quit:line=""  if $get(TempAddress(cnt,line))'="" set linefound=1
	..if 'linefound quit
	.. // set temporary foreign address
	..set TempAddress(cnt,"FOREIGN")="" if $get(TempAddress(cnt,"FOR-CTY"))'=""!($get(TempAddress(cnt,"FOR-PST"))'="")!($get(TempAddress(cnt,"FOR-COU"))'="") set TempAddress(cnt,"FOREIGN")=$get(TempAddress(cnt,"FOR-CTY"))_"&"_$get(TempAddress(cnt,"FOR-PST"))_"&"_$get(TempAddress(cnt,"FOR-COU"))
	..set Address("TEMP")=$get(Address("TEMP"))_$select($length(Address("TEMP")):"~",1:"")_$get(TempAddress(cnt,"LINE1"))_"^"_$get(TempAddress(cnt,"LINE2"))_"^"_$get(TempAddress(cnt,"CTY"))_"^"_$get(TempAddress(cnt,"PRV"))_"^"_$get(TempAddress(cnt,"PST"))_"^^"_$get(TempAddress(cnt,"TYP"))_"^"_$get(TempAddress(cnt,"FOREIGN"))_"^^^"
 
	// set counter
	set DATA("PID",1)=$increment(count("PID"))
 
	// build patient numbers
	for type="INT","REG","HOSP","CHINHS","MC","DVA","GOV","SN","HC" if $data(PatNum(type)),$data(number(type)) do
	.if type="HOSP" do
	.. // ignore second PID for A37 as hospital number string is already built
	..if msgtype["A37",(intRegMrn=intRegTo) quit
	..set (number(type),found)=""
	..set intHosp="" for  set intHosp=$order(number(type,intHosp)) quit:intHosp=""  do
	...set num=number(type,intHosp),active=$$ActiveHospMrn^HL7OutExtract1(intHosp)
	...set data=$piece(num,"^",6),$piece(data,"&",2)=$select(active="Y":"A",1:"I"),$piece(num,"^",6)=data,number(type,intHosp)=num
	...if intAdmNum'="" set number(type)=number(type)_$select($length(number(type)):"~",1:"")_number(type,intHosp) quit
	...if intAdmNum="",intRegMrn'="" do
	....if active'="Y",msgtype'["A47" quit
	....if msgtype["A47"!(msgtype["A43") do
	..... // extract status for hospital number
	.....if $get(old("RTMASTER",2))=$piece(num,"^") set found=1
	...if active'="Y",(msgtype["A40"!(msgtype["A43")) quit
	...set number(type)=number(type)_$select($length(number(type)):"~",1:"")_number(type,intHosp)
	...if trigger="Delete",$get(old("RTMASTER",1))=intHosp do
	....do NumTypeDetail^HL7Common2(PatNum(type),"","",.asstype,.asscode,.idcode,.idmrtype)
	....set hospmrn=$get(old("RTMASTER",2)),active=$get(old("RTMASTER",13)) if trigger="Delete" set active="D"
	....if hospmrn="" quit
	....set code="" kill rowid("type") set mrtype=$get(old("RTMASTER",9))
	....if idmrtype'="Y",mrtype'="" set code=$piece($get(^RTC("TYPE",mrtype)),"^",1)
	....if idmrtype="Y",mrtype'="" set code=$$MedRecType^HL7OutExtract1(mrtype),idcode=$piece($get(^RTC("TYPE",mrtype)),"^",1)
	....set num=hospmrn,$piece(num,"^",asstype)=code,$piece(num,"^",5)=idcode,$piece(num,"^",6)=$piece($piece(num,"^",6),"&")_"&"_$select(active="Y":"A",active="D":"D",1:"I")
	....set number(type)=number(type)_$select($length(number(type)):"~",1:"")_num
	.if $translate($get(number(type)),"^~")="" quit
	.do NumTypeDetail^HL7Common2(PatNum(type),.segtype,.field,"","","","","","",.expfield,.expformat)
	.if expfield'="" set $piece(number(type),"^",expfield)=$select($length(expformat):$extract($get(number(type,"EXPDATE")),1,$length(expformat)),1:$get(number(type,"EXPDATE")))
	.set DATA(segtype,field)=$get(DATA(segtype,field))_$select($length($get(DATA(segtype,field))):"~",1:"")_number(type)
 
	set DATA("PID",5)=name if $length(dob) set DATA("PID",7)=$piece(dob,"^")_"^"_$select(estdob="Y":"Y",estdob="""""":"""""",1:"")
	set DATA("PID",8)=$piece(sex,"^"),DATA("PID",9)=Alias,DATA("PID",10)=indigenous,DATA("PID",11)=Address("PERM")_$select($length(Address("TEMP")):"~",1:"")_Address("TEMP")
	set DATA("PID",13)=Phone13,DATA("PID",14)=Phone14,DATA("PID",15)=language,DATA("PID",16)=marital,DATA("PID",17)=religion,DATA("PID",21)=MotherNum,DATA("PID",23)=country
	if $length(death) do  else  kill DATA("PID",29),DATA("PID",30)
	.set DATA("PID",29)=death,DATA("PID",30)=$select(death="""""":"""""",1:"Y")
 
	if msgtype["A37",'$length($get(intRegCheck)) set intRegCheck=intRegMrn
 
	kill PatDetail,PatDetailx,PermAddress,TempAddress,Address,number,phone
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
	quit 0
 
CheckNK1 ;; build DATA array - NK1
NK1()	set intRegMrn=$get(intRegMrn),count="" kill DATA,Repeat
 
	set check=1 if $order(^PAPER(intRegMrn,"NOK",0))="" set check=""
	if mandatory'="Y",'check quit 0
	if mandatory="Y",'check set DATA("NK1",1)=1 quit 0
 
	set seq=0 for  set seq=$order(^PAPER(intRegMrn,"NOK",seq)) quit:seq=""  do  if count,repeat'="Y" quit
	. // extract next of kin detail
	.set nokrowid=intRegMrn_"||"_seq
	.kill NextOfKin if $$NextOfKin^HL7OutExtract1(nokrowid,.NextOfKin) quit
	.set (name,sex,phoneH,phoneW,phoneM,email)="" kill add,permadd,tempadd,phone
	.set intNokNum=$get(NextOfKin(27))
 
	.if intNokNum'="" do
	..kill PatDetail if $$PatDetail^HL7OutExtract1(intNokNum,.PatDetail) quit
	..do BuildAddress^HL7OutExtract1
	..set mtable="PA_Person" do CodeTable^HL7OutExtract1A(mtable)
	..set name=$$BuildName^HL7Common3($get(PatDetail(4)),$get(PatDetail(188)),$get(PatDetail(118)))
	..set $piece(name,%1,5)=$piece($get(PatDetail(139)),"^"),$piece(name,%1,7)="L"
	..set sex=$get(PatDetail(15)),phoneW=$get(PatDetail(36)),phoneH=$get(PatDetail(38)),email=$get(PatDetail(138)),phoneM=$get(PatDetail(140))
 
	.if intNokNum="" do
	..kill PermAddress
	..set PermAddress("LINE1")=$get(NextOfKin(46)),PermAddress("LINE2")=$get(NextOfKin(41)),PermAddress("PRV")=$get(NextOfKin(50)),PermAddress("PST")=$get(NextOfKin(51)),PermAddress("CTY")=$get(NextOfKin(52))
	..set PermAddress("FOR-COU")=$get(NextOfKin(54)),PermAddress("FOR-CTY")=$get(NextOfKin(55)),PermAddress("FOR-PST")=$get(NextOfKin(56))
	..set PatDetail(139)=NextOfKin(49)
	..set mtable="PA_Person" do CodeTable^HL7OutExtract1A(mtable)
	..set NextOfKin(49)=PatDetail(139) kill PatDetail
	..set name=$$BuildName^HL7Common3($get(NextOfKin(43)),$get(NextOfKin(44)),$get(NextOfKin(45)))
	..set $piece(name,%1,5)=$piece($get(NextOfKin(49)),"^"),$piece(name,%1,7)="L"
	..set email=$get(NextOfKin(40)),phoneM=$get(NextOfKin(42)),phoneH=$get(NextOfKin(47)),phoneW=$get(NextOfKin(48))
 
	.set mtable="PA_Nok" do CodeTable^HL7OutExtract1A(mtable)
	.set relation=$get(NextOfKin(8)),contact=$get(NextOfKin(30))
	.set datefrom=$select($get(NextOfKin(28))'="":$$Date^HL7(NextOfKin(28)),1:""),dateto=$select($get(NextOfKin(29))'="":$$Date^HL7(NextOfKin(29)),1:"")
	.set firstcontact=$get(NextOfKin(39)),firstcontact=$select(firstcontact="Y":"FC",1:"")
	.if $get(sex)="" set sex=$get(NextOfKin(53))
 
	. // setup contact fields
	.set (homephone,workphone)=""
	.if $length(phoneW) set workphone=workphone_$select($length(workphone):"~",1:"")_phoneW_"^WPN^PH^^^^^^P"
	.if $length(phoneH) set homephone=homephone_$select($length(homephone):"~",1:"")_phoneH_"^PRN^PH^^^^^^P"
	.if $length(phoneM) set homephone=homephone_$select($length(homephone):"~",1:"")_phoneM_"^ORN^CP^^^^^^P"
	.if $length(email) set homephone=homephone_$select($length(homephone):"~",1:"")_"^NET^Internet^"_email
	.set phtype="" for  set phtype=$order(phone(phtype)) quit:phtype=""  do
	..set phcnt="" for  set phcnt=$order(phone(phtype,phcnt)) quit:phcnt=""  do
	...set homephone=homephone_$select($length(homephone):"~",1:"")_$select(phone(phtype,phcnt)'="":phone(phtype,phcnt)_"^PRN^PH^^^^^^"_phtype,1:"")
 
	. // setup permanent/temp address
	.kill Address set Address=""
	.if $data(PermAddress) do
	..for k="CTY","PRV","PST","FOR-COU" set PermAddress(k)=$piece($$Address^HL7Common7(.reject,"O",$select(k="FOR-COU":"COU",1:k),$get(PermAddress(k))),"|",$select(k="CTY":2,1:1))
	.. // check if permanent address has details
	..set (line,linefound)="" for  set line=$order(PermAddress(line)) quit:line=""  if $get(PermAddress(line))'="" set linefound=1
	..if 'linefound quit
	.. // set  foreign address
	..set PermAddress("FOREIGN")="" if $get(PermAddress("FOR-CTY"))'=""!($get(PermAddress("FOR-PST"))'="")!($get(PermAddress("FOR-COU"))'="") set PermAddress("FOREIGN")=$get(PermAddress("FOR-CTY"))_"&"_$get(PermAddress("FOR-PST"))_"&"_$get(PermAddress("FOR-COU"))
	..set Address=$get(PermAddress("LINE1"))_"^"_$get(PermAddress("LINE2"))_"^"_$get(PermAddress("CTY"))_"^"_$get(PermAddress("PRV"))_"^"_$get(PermAddress("PST"))_"^^P^"_$get(PermAddress("FOREIGN"))_"^^^"
	.if $data(TempAddress) do
	..set cnt="" for  set cnt=$order(TempAddress(cnt)) quit:cnt=""  do
	...for k="CTY","PRV","PST","TYP","FOR-COU" set TempAddress(cnt,k)=$piece($$Address^HL7Common7(.reject,"O",$select(k="FOR-COU":"COU",1:k),$get(TempAddress(cnt,k))),"|",$select(k="CTY":2,1:1))
	... // check if temporary address has details
	...set (line,linefound)="" for  set line=$order(TempAddress(cnt,line)) quit:line=""  if $get(TempAddress(cnt,line))'="" set linefound=1
	...if 'linefound quit
	.. // set temporary foreign address
	...set TempAddress(cnt,"FOREIGN")="" if $get(TempAddress(cnt,"FOR-CTY"))'=""!($get(TempAddress(cnt,"FOR-PST"))'="")!($get(TempAddress(cnt,"FOR-COU"))'="") set TempAddress(cnt,"FOREIGN")=$get(TempAddress(cnt,"FOR-CTY"))_"&"_$get(TempAddress(cnt,"FOR-PST"))_"&"_$get(TempAddress(cnt,"FOR-COU"))
	...set Address=$get(Address)_$select($length(Address):"~",1:"")_$get(TempAddress(cnt,"LINE1"))_"^"_$get(TempAddress(cnt,"LINE2"))_"^"_$get(TempAddress(cnt,"CTY"))_"^"_$get(TempAddress(cnt,"PRV"))_"^"_$get(TempAddress(cnt,"PST"))_"^^"_$get(TempAddress(cnt,"TYP"))_"^"_$get(TempAddress(cnt,"FOREIGN"))_"^^^"
 
	.set count=count+1
 
	.if count=1 do
	..set DATA("NK1",1)=count,DATA("NK1",2)=name,DATA("NK1",3)=relation,DATA("NK1",4)=Address,DATA("NK1",5)=homephone
	..set DATA("NK1",6)=workphone,DATA("NK1",7)=contact,DATA("NK1",8)=datefrom,DATA("NK1",9)=dateto,DATA("NK1",15)=$piece(sex,"^")
	..set DATA("NK1",29)=firstcontact
	.if count>1,repeat="Y" do
	..set Repeat("NK1",count,1)=count,Repeat("NK1",count,2)=name,Repeat("NK1",count,3)=relation,Repeat("NK1",count,4)=Address
	..set Repeat("NK1",count,5)=homephone,Repeat("NK1",count,6)=workphone,Repeat("NK1",count,7)=contact,Repeat("NK1",count,8)=datefrom
	..set Repeat("NK1",count,9)=dateto,Repeat("NK1",count,15)=$piece(sex,"^"),Repeat("NK1",count,29)=firstcontact
 
	kill PatDetail,NextOfKin,Address,PermAddress,TempAddress,phone
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
	quit 0



