^MAC^Save for Source Control^^~Format=IRIS.S~^RAW
%RO
HL7InboundADTA01^MAC^^^0
HL7InboundADTA01	// ML 22/03/2011 ;  HL7 interface : patient event ADT_A01
SrcVer ;; $Id: //trak/main/releases/T2020/1/CLXX/rtn/hl7/HL7InboundADTA01.rtn#1 $
	quit
	// A01 - admit a patient
A01()
	set HL7Flag="Y"
	set (reject,rejecthere)=""
	// reject if internal rowid is expected as this is not correct for a new admission
	if HL7("INT-ADM-NUM")="Y" {
		set reject="Unable to receive internal admission number for ADT_A01 message",reject("Detail")="PV1^^19~205"
		do Reject^HL7Reject(reject,"R")
	}
	if reject'="" quit $select(reject="":"AA",1:"AR\"_reject_"\"_$piece($get(reject("Detail")),"~")_"\"_$piece($get(reject("Detail")),"~",2))
	// check if message forwarding required
	set forward=$$MsgForward^HL7Inbound(msgtype)
	set cnt1=0 for  {
		if reject'="" quit
		set cnt1=$order(^TMP("HL7",$job,cnt1)) if cnt1="" quit
		if ",MSH,MSA,EVN,QRD,QRF,QAK,"[(","_cnt1_",") quit
		if '$data(^TMP("HL7",$job,cnt1,"PID")) set reject="Required segment PID not received in message",reject("Detail")="PID^^^~"_100,rejecthere="Y" quit
		// check if pre-existing patient
		set numbers=$$RegNum^HL7Common2(.reject,"A01")
		set RegMrn=$piece(numbers,"|"),intRegMrn=$piece(numbers,"|",2)
		if reject'="" set rejecthere="Y" quit
		// extract EVN information
		do EVN^HL7InboundExtract0
		kill PERSON
		// extract PID information
		do PID^HL7InboundExtract1 if reject'="" quit
		set foundPV1=""
		set cnt2=0 for  {
			if reject'="" quit
			set cnt2=$order(^TMP("HL7",$job,cnt1,cnt2)) if cnt2="" quit
			kill PAADM,PAADMINS,DIAG,SQL
			
			set reject=""
			// ignore segment codes in cnt2
			if $data(^TMP("HL7",$job,cnt1,cnt2,"PV1")) set foundPV1=1
			if foundPV1,$extract(cnt2,1,2)?1.2A quit
			if 'foundPV1,'$data(^TMP("HL7",$job,cnt1,cnt2,"PV1")) set reject="Required segment PV1 not received in message",reject("Detail")="PV1^^^~"_100,rejecthere="Y" quit
			kill PLIST
			// extract PV1 information
			do PV1^HL7InboundExtract2
			if PAADM("PAADMADMNo")="" set reject="No admission number received, unable to create admission",reject("Detail")="PV1^"_$get(^TMP("HL7",$job,cnt1,cnt2,"PV1",1))_"^19~205",rejecthere="Y" quit
			if $$intAdmNum^HL7Common2(PAADM("PAADMADMNo"))'="" set reject="Admission number "_PAADM("PAADMADMNo")_" already exists",reject("Detail")="PV1^"_$get(^TMP("HL7",$job,cnt1,cnt2,"PV1",1))_"^19~205",rejecthere="Y" quit
			// check if bed occupied
			if PAADM("BedID")'="",##class(web.PAAdmTransaction).IsBedFree(PAADM("BedID")," T ","")=1 set reject="Ward/room/bed combination "_PAADM("WARDDesc")_"/"_PAADM("ROOMDesc")_"/"_PAADM("BEDCode")_" received for episode number "_PAADM("PAADMADMNo")_" already occupied",reject("Detail")="PV1^"_$get(^TMP("HL7",$job,cnt1,cnt2,"PV1",1))_"^3~206",rejecthere="Y" quit
			// extract PV2 information
			do PV2^HL7InboundExtract2
			// extract IN1 information
			do IN1^HL7InboundExtract2
			// extract DG1 information
			do DG1^HL7InboundExtract2
			// extract ZV1 information
			do ZV1^HL7InboundExtractZ
			set reject=""
			// add admission information
			set intAdmNum=""
			if $get(PAADM("PAADMType"))'="E" do PAADM^HL7InboundUpdate2
			// add emergency admission information
			if $get(PAADM("PAADMType"))="E" do PAADMEMR^HL7InboundUpdate2
			if reject'="" quit
			// extract MR_Adm rowid
			&SQL(SELECT PAADM_MainMRADM_DR INTO :intMRAdm FROM PA_Adm WHERE PAADM_RowId = :intAdmNum)
			// update non-api fields
			kill PLIST
			if $data(SQL) {
				set var="" for  {
					set var=$order(SQL(var)) if var="" quit
					if $get(SQL(var))'="" {
						if SQL(var)="""""" set SQL(var)=""
						set j=$piece(var,"-")
						set PLIST(j)=SQL(var)
					}
				}
				// manually update fields not processed by api
				if $data(PLIST) {
					&SQL(UPDATE PA_Adm VALUES :PLIST() WHERE PAADM_RowId = :intAdmNum)
				}
			}
			// add insurance information
			do INSURANCE^HL7InboundUpdate2
			if reject'="" quit
			// add diagnosis information
			do DIAGNOSIS^HL7InboundUpdate2
			// check if message forwarding required
			if forward,reject="" set forwardnum(cnt1_"-"_cnt2,intAdmNum)="PAADM"
		}
		kill PERSON,PAADM,PAADMINS,DIAG,SQL
	}
	kill PERSON,PAADM,PAADMINS,DIAG,SQL
	if rejecthere="Y" do Reject^HL7Reject(reject,"R")
	quit $select(reject="":"AA",1:"AR\"_reject_"\"_$piece($get(reject("Detail")),"~")_"\"_$piece($get(reject("Detail")),"~",2))



