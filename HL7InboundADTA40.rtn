^MAC^Save for Source Control^^~Format=IRIS.S~^RAW
%RO
HL7InboundADTA40^MAC^^^0
HL7InboundADTA40	// ML 18/08/2011 ;  HL7 interface : patient event ADT_A40
	quit
	// A40 - merge patient information - patient identifier list
A40()
	set HL7Flag="Y"
	set (reject,rejecthere)=""
	// check if message forwarding required
	set forward=$$MsgForward^HL7Inbound(msgtype)
	set cnt1=0 for  {
		if reject'="" quit
		set cnt1=$order(^TMP("HL7",$job,cnt1)) if cnt1="" quit
		if ",MSH,MSA,EVN,QRD,QRF,QAK,"[(","_cnt1_",") quit
		if '$data(^TMP("HL7",$job,cnt1,"PID")) set reject="Required segment PID not received in message",reject("Detail")="PID^^^~"_100,rejecthere="Y" quit
		if '$data(^TMP("HL7",$job,cnt1,"MRG")) set reject="Required segment MRG not received in message",reject("Detail")="MRG^^^~"_100,rejecthere="Y" quit
		// check if pre-existing patient
		set numbers=$$RegNum^HL7Common2(.reject,"A40")
		set RegMrn=$piece(numbers,"|"),intRegMrn=$piece(numbers,"|",2)
		if reject'="" set rejecthere="Y" quit
		// extract EVN information
		do EVN^HL7InboundExtract0
		kill PERSON,MERGE,HOSPMRN,TEMPADD,TADD,ALIAS
		// extract PID information
		do PID^HL7InboundExtract1 if reject'="" quit
		set PERSON("RegistrationNumber")=RegMrn
		// extract MRG information
		do MRG^HL7InboundExtract1 if reject'="" quit
		set MERGE("TOPatientID")=intRegMrn,MERGE("TOPAPMIRegNo")=RegMrn
		// extract PD1 information
		do PD1^HL7InboundExtract1
		set reject=""
		// merge patients
		do MERGE^HL7InboundUpdate1
		if reject'="" quit
		// update person information
		do PERSON^HL7InboundUpdate1
		if reject'="" quit
		// update temporary addresses
		do TEMPADDRESS^HL7InboundUpdate1
		if reject'="" quit
		// update med record information
		do HOSPMRN^HL7InboundUpdate1
		if reject'="" quit
		// update alias information
		do ALIAS^HL7InboundUpdate1
		if reject'="" quit
		// check if message forwarding required
		if forward,reject="" set forwardnum(cnt1,MERGE("TOPatientID")_"||"_MERGE("FROMPatientID"))=""
		kill PERSON,MERGE,HOSPMRN,TEMPADD,TADD,ALIAS
	}
	kill PERSON,MERGE,HOSPMRN,TEMPADD,TADD,ALIAS
	if rejecthere="Y" do Reject^HL7Reject(reject,"R")
	quit $select(reject="":"AA",1:"AR\"_reject_"\"_$piece($get(reject("Detail")),"~")_"\"_$piece($get(reject("Detail")),"~",2))



