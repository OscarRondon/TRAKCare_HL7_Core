^MAC^Save for Source Control^^~Format=IRIS.S~^RAW
%RO
HL7SiteQLDCreate1^MAC^^^0
HL7SiteQLDCreate1 // ML 06/05/2003 ; HL7 interface : create transmission packets - Queensland Health
SrcVer ;; $Id: //trak/main/releases/T2020/1/CLXX/rtn/hl7/HL7SiteQLDCreate1.rtn#1 $
	quit
 
CheckPID ;; build data array - PID
PID()	set intRegMrn=$get(intRegMrn),intAdmNum=$get(intAdmNum) kill DATA
 
	if msgtype["A37",$length($get(intRegCheck)) do
	.if intRegCheck'=intRegTo set intRegMrn=intRegTo
 
	// extract patient detail
	kill PatDetail,PatDetailx set SQLCODE=$$PatDetail^HL7OutExtract1(intRegMrn,.PatDetail,.PatDetailx)
	if SQLCODE quit SQLCODE
 
	// check for new null fields and change to ""
	for xtable="PAPER","PAPMI" if $data(old(xtable)) do NullChange^HL7Common3(xtable)
 
	// extract patient address detail
	do BuildAddress^HL7OutExtract1
 
	// extract code table detail
	for mtable="PA_Person","PA_PatMas" do CodeTable^HL7OutExtract1A(mtable)
 
	// extract patient number detail
	if msgtype'["A37" do  if SQLCODE quit SQLCODE
	.kill number set SQLCODE=$$PatNum^HL7OutExtract1(intRegMrn,intAdmNum,.number)
 
	if msgtype["A37" do  if SQLCODE quit SQLCODE
	.kill number set SQLCODE=$$PatNum^HL7OutExtract1(intRegMrn,intAdmNum,.number)
	. // extract numbers required for first PID
	.if intRegMrn'=intRegTo do  quit
	..set intMergeNum=$order(^PAMRi("PAT-FROM",intRegTo,"")) if intMergeNum'="" do
	...set row="" for  set row=$order(^PAMR(intMergeNum,"DET",row)) quit:row=""  if $piece(^PAMR(intMergeNum,"DET",row),"^")="RTMAS" do
	....set intHospFrom=$piece($piece(^PAMR(intMergeNum,"DET",row),"^",2),":") if intHospFrom="" quit
	....kill number("HOSP",intHospFrom)
	. // extract numbers required for second PID/mrn's moved
	.set intMergeNum=$order(^PAMRi("PAT-FROM",intRegMrn,"")) if intMergeNum'="" do
	..set row="" for  set row=$order(^PAMR(intMergeNum,"DET",row)) quit:row=""  if $piece(^PAMR(intMergeNum,"DET",row),"^")="RTMAS" do
	...set intHospFrom=$piece($piece(^PAMR(intMergeNum,"DET",row),"^",2),":") if intHospFrom="" quit
	...set active=$piece($piece(^PAMR(intMergeNum,"DET",row),"^",2),":",2)
	...if '$data(^RT(intHospFrom)) quit
	...do NumTypeDetail^HL7Common2(PatNum("HOSP"),"","",.asstype,.asscode,.idcode,.idmrtype)
	...do MedRecord^HL7OutExtract1(intHospFrom)
	...set num=$piece(^RT(intHospFrom),"^"),$piece(num,"^",asstype)=code,$piece(num,"^",5)=idcode,$piece(num,"^",6)=$piece($piece(num,"^",6),"&")_"&"_$select(active="Y":"A",active="D":"D",1:"I")
	...set number("HOSP")=$get(number("HOSP"))_$select($length($get(number("HOSP"))):"~",1:"")_num
 
	// extract patient alias
	kill alias set Alias="" for altype="ALIAS","SUR" do 
	.set row=0 for  set row=$order(^PAPER(intRegMrn,altype,row)) quit:row=""  do
	..set intAliasNum=intRegMrn_"||"_row
	..set al=$$PatAlias^HL7OutExtract1(intAliasNum,altype) if $translate($piece(al,"|"),",")="",($translate($piece(al,"|"),"_")="") quit
	..set sep="," if $length($piece(al,"|"),"_")=7 set sep="_"
	..set active=$piece(al,"|",2),al=$piece(al,"|",1),$piece(al,sep,7)="A&"_$select(active="Y":"A",1:"I"),alias(al)=""
	if HL7("VER")'="2.4" do
	.set al="" for  set al=$order(alias(al)) quit:al=""  do
	..set sep="," if $length($piece(al,"|"),"_")=7 set sep="_"
	..set Alias=Alias_$select($length(Alias):"~",1:"")_$translate($piece(al,sep,1,2),",","^")
	if HL7("VER")="2.4" do
	.set al="" for  set al=$order(alias(al)) quit:al=""  do
	..set sep="," if $length($piece(al,"|"),"_")=7 set sep="_"
	..set Alias=Alias_$select($length(Alias):"~",1:"")_$translate(al,sep,"^")
 
	set sex=$get(PatDetail(15)),marital=$get(PatDetail(20)),religion=$get(PatDetail(23)),phoneW=$get(PatDetail(36)),phoneH=$get(PatDetail(38)),country=$get(PatDetail(77))
	set email=$get(PatDetailx(130)),title=$get(PatDetail(139)),phoneM=$get(PatDetailx(131)),indigenous=$get(PatDetail(141)),language=$get(PatDetail(143)),ethnic=$get(PatDetail(180))
	set MotherNum="" if $get(PatDetail(128))'="" set MotherNum=$$extRegMrn^HL7Common2(PatDetail(128))
	// check dates for null values
	set dob=$get(PatDetail(11)) if dob'="",dob'="""""" do
	.set dob=$$DateConv^HL7Common2(.reject,"IE",PatDetail(11),"B"),estdob=$get(PatDetail(152))
	set death=$get(PatDetail(74)) if death'="",death'="""""" do
	.set death=$$Date^HL7(PatDetail(74)_","_PatDetail(82))
 
	// set counter
	set DATA("PID",1)=$increment(count("PID"))
 
	// adjust name
	set name=$$BuildName^HL7Common3($get(PatDetail(4)),$get(PatDetail(5)),$get(PatDetail(118)))
	set $piece(name,%1,5)=$piece(title,"^")_%1_%1_"L",DATA("PID",5)=name
 
	// build patient numbers
	for type="INT","REG","HOSP","CHINHS","MC","DVA","GOV","SN","HC" if $data(PatNum(type)),$data(number(type)) do
	.if type="HOSP" do
	.. // ignore second PID for A37 as hospital number string is already built
	..if msgtype["A37",(intRegMrn=intRegTo) quit
	..set (number(type),found)=""
	..set intHosp="" for  set intHosp=$order(number(type,intHosp)) quit:intHosp=""  do
	...set num=number(type,intHosp),active=$$ActiveHospMrn^HL7OutExtract1(intHosp)
	...set data=$piece(num,"^",6),$piece(data,"&",2)=$select(active="Y":"A",1:"I"),$piece(num,"^",6)=data,number(type,intHosp)=num
	...if intAdmNum'="" set number(type)=number(type)_$select($length(number(type)):"~",1:"")_number(type,intHosp) quit
	...if intAdmNum="",intRegMrn'="" do
	....if active'="Y",msgtype'["A47" quit
	....if msgtype["A47"!(msgtype["A43") do
	..... // extract status for hospital number
	.....if $get(old("RTMASTER",2))=$piece(num,"^") set found=1
	...if active'="Y",(msgtype["A40"!(msgtype["A43")) quit
	...set number(type)=number(type)_$select($length(number(type)):"~",1:"")_number(type,intHosp)
	...if trigger="Delete",$get(old("RTMASTER",1))=intHosp do
	....do NumTypeDetail^HL7Common2(PatNum(type),"","",.asstype,.asscode,.idcode,.idmrtype)
	....set hospmrn=$get(old("RTMASTER",2)),active=$get(old("RTMASTER",13)) if trigger="Delete" set active="D"
	....if hospmrn="" quit
	....set code="" kill rowid("type") set mrtype=$get(old("RTMASTER",9))
	....if idmrtype'="Y",mrtype'="" set code=$piece($get(^RTC("TYPE",mrtype)),"^",1)
	....if idmrtype="Y",mrtype'="" set code=$$MedRecType^HL7OutExtract1(mrtype),idcode=$piece($get(^RTC("TYPE",mrtype)),"^",1)
	....set num=hospmrn,$piece(num,"^",asstype)=code,$piece(num,"^",5)=idcode,$piece(num,"^",6)=$piece($piece(num,"^",6),"&")_"&"_$select(active="Y":"A",active="D":"D",1:"I")
	....set number(type)=number(type)_$select($length(number(type)):"~",1:"")_num
	.if $translate($get(number(type)),"^~")="" quit
	.do NumTypeDetail^HL7Common2(PatNum(type),.segtype,.field,"","","","","","",.expfield,.expformat)
	.if expfield'="" set $piece(number(type),"^",expfield)=$select($length(expformat):$extract($get(number(type,"EXPDATE")),1,$length(expformat)),1:$get(number(type,"EXPDATE")))
	.set DATA(segtype,field)=$get(DATA(segtype,field))_$select($length($get(DATA(segtype,field))):"~",1:"")_number(type)
 
	// setup date fields
	if $length(dob) set DATA("PID",7)=$piece(dob,"^")_"^"_$select(estdob="Y":"Y",1:"")
	if $length(death) do  else  kill DATA("PID",29),DATA("PID",30)
	.set DATA("PID",29)=death,DATA("PID",30)="Y"
 
	// setup contact fields
	set DATA("PID",13)=""
	if HL7("VER")'="2.4" set DATA("PID",13)=phoneH,DATA("PID",14)=phoneW
	if HL7("VER")="2.4" do
	.if msgtype["A19" do  quit
	..if $length(phoneH) set DATA("PID",13)=phoneH_%1_"PRN"_%1_%1_%1_%1_%1_phoneH
	..if $length(phoneW) set DATA("PID",14)=phoneW_%1_"WPN"_%1_%1_%1_%1_%1_phoneW
	.if $length(phoneH) set DATA("PID",13)=DATA("PID",13)_$select($length(DATA("PID",13)):"~",1:"")_phoneH_%1_"PRN"_%1_"PH"_%1_%1_%1_%1_%1_%1_"P"
	.if $length(phoneW) set DATA("PID",13)=DATA("PID",13)_$select($length(DATA("PID",13)):"~",1:"")_phoneW_%1_"WPN"_%1_"PH"_%1_%1_%1_%1_%1_%1_"P"
	.if $length(phoneM) set DATA("PID",13)=DATA("PID",13)_$select($length(DATA("PID",13)):"~",1:"")_phoneM_%1_"ORN"_%1_"CP"_%1_%1_%1_%1_%1_%1_"P"
	.if $length(email) set DATA("PID",13)=DATA("PID",13)_$select($length(DATA("PID",13)):"~",1:"")_%1_"NET"_%1_"Internet"_%1_email
	.set phtype="" for  set phtype=$order(phone(phtype)) quit:phtype=""  do
	..set PhoneType=$piece($$Address^HL7Common7(.reject,"O","TYP",phtype),"|")
	..set phcnt="" for  set phcnt=$order(phone(phtype,phcnt)) quit:phcnt=""  do
	...set DATA("PID",13)=DATA("PID",13)_$select($length(DATA("PID",13)):"~",1:"")_phone(phtype,phcnt)_%1_"PRN"_%1_"PH"_%1_%1_%1_%1_%1_%1_PhoneType
 
	// setup code table fields : sex, indigenous status, language, marital status, religion, ethnic code
	set DATA("PID",8)=$piece(sex,"^"),DATA("PID",9)=Alias,DATA("PID",10)=indigenous,DATA("PID",15)=language,DATA("PID",16)=marital
	set DATA("PID",17)=religion,DATA("PID",21)=MotherNum,DATA("PID",22)=ethnic,DATA("PID",23)=country
 
	// setup permanent/temp address
	if $data(PermAddress) do
	.for k="CTY","PRV","PST" set PermAddress(k)=$piece($$Address^HL7Common7(.reject,"O",k,$get(PermAddress(k))),"|",$select(k="CTY":2,1:1))
	.set DATA("PID",11)=$get(PermAddress("LINE1"))_%1_$get(PermAddress("LINE2"))_%1_$get(PermAddress("CTY"))_%1_$get(PermAddress("PRV"))_%1_$get(PermAddress("PST"))_%1_%1_"P"_%1_%1_%1_%1
	if $data(TempAddress) do
	.set cnt="" for  set cnt=$order(TempAddress(cnt)) quit:cnt=""  do
	..for k="CTY","PRV","PST","TYP","FOR-COU" set TempAddress(cnt,k)=$piece($$Address^HL7Common7(.reject,"O",$select(k="FOR-COU":"COU",1:k),$get(TempAddress(cnt,k))),"|",$select(k="CTY":2,1:1))
	..set DATA("PID",11)=$get(DATA("PID",11))_$select($length(DATA("PID",11)):"~",1:"")_$get(TempAddress(cnt,"LINE1"))_%1_$get(TempAddress(cnt,"LINE2"))_%1_$get(TempAddress(cnt,"CTY"))_%1_$get(TempAddress(cnt,"PRV"))_%1_$get(TempAddress(cnt,"PST"))_%1_%1_$get(TempAddress(cnt,"TYP"))_%1_$get(TempAddress(cnt,"FOR-CTY"))_%4_$get(TempAddress(cnt,"FOR-PST"))_%4_$get(TempAddress(cnt,"FOR-COU"))_%1_%1_%1
 
	if msgtype["A37",'$length($get(intRegCheck)) set intRegCheck=intRegMrn
 
	kill PatDetail,PatDetailx,PermAddress,TempAddress,number,phone
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
	quit 0
 
CheckNK1 ;; build DATA array - NK1
NK1()	set intRegMrn=$get(intRegMrn),count="" kill DATA,Repeat
 
	set check=1 if $order(^PAPER(intRegMrn,"NOK",0))="" set check=""
	if mandatory'="Y",'check quit 0
	if mandatory="Y",'check set DATA("NK1",1)=1 quit 0
 
	set seq=0 for  set seq=$order(^PAPER(intRegMrn,"NOK",seq)) quit:seq=""  do  if count,repeat'="Y" quit
	. // extract next of kin detail
	.set nokrowid=intRegMrn_"||"_seq
	.kill NextOfKin if $$NextOfKin^HL7OutExtract1(nokrowid,.NextOfKin) quit
	.set (name,sex,phoneH,phoneW,phoneM,email)="" kill add,permadd,tempadd,phone
	.set intNokNum=$get(NextOfKin(27))
 
	.if intNokNum'="" do
	..kill PatDetail if $$PatDetail^HL7OutExtract1(intNokNum,.PatDetail) quit
	..do BuildAddress^HL7OutExtract1
	..set mtable="PA_Person" do CodeTable^HL7OutExtract1A(mtable)
	..if HL7("VER")="2.4" do
	...set name=$$BuildName^HL7Common3($get(PatDetail(4)),$get(PatDetail(5)),$get(PatDetail(118)))
	...set $piece(name,%1,5)=$piece($get(PatDetail(139)),"^")
	...if msgtype["A19" do
	....set name=$get(PatDetail(5))_$select($get(PatDetail(118))'="":" "_PatDetail(118),1:"")_" "_$get(PatDetail(4))
	...set $piece(name,%1,7)="L"
	..if HL7("VER")="2.1",msgtype["A19" do
	...set name=$get(PatDetail(5))_$select($get(PatDetail(118))'="":" "_PatDetail(118),1:"")_" "_$get(PatDetail(4))
	...set $piece(name,%1,6)=name
	..set sex=$get(PatDetail(15)),phoneW=$get(PatDetail(36)),phoneH=$get(PatDetail(38)),email=$get(PatDetail(138)),phoneM=$get(PatDetail(140))
 
	.if intNokNum="" do
	..kill PermAddress
	..set PermAddress("LINE1")=$get(NextOfKin(46)),PermAddress("LINE2")=$get(NextOfKin(41)),PermAddress("PRV")=$get(NextOfKin(50)),PermAddress("PST")=$get(NextOfKin(51)),PermAddress("CTY")=$get(NextOfKin(52))
	..set PermAddress("FOR-COU")=$get(NextOfKin(54)),PermAddress("FOR-CTY")=$get(NextOfKin(55)),PermAddress("FOR-PST")=$get(NextOfKin(56))
	.. // convert escape sequences
	..for line="LINE1","LINE2" do
	... // strip leading spaces
	...for i=1:1 quit:$extract(PermAddress(line))'=" "  set PermAddress(line)=$extract(PermAddress(line),2,$length(PermAddress(line)))
 	...set PermAddress(line)=$$TextConversion^HL7Common12(PermAddress(line),"O")
 
	..set PatDetail(139)=NextOfKin(49)
	..set mtable="PA_Person" do CodeTable^HL7OutExtract1A(mtable)
	..set NextOfKin(49)=PatDetail(139) kill PatDetail
	..if HL7("VER")="2.4" do
	...set name=$$BuildName^HL7Common3($get(NextOfKin(43)),$get(NextOfKin(44)),$get(NextOfKin(45)))
	...set $piece(name,%1,5)=$piece($get(NextOfKin(49)),"^")
	...if msgtype["A19" do
	....set name=$get(NextOfKin(44))_$select($get(NextOfKin(45))'="":" "_NextOfKin(45),1:"")_" "_$get(NextOfKin(43))
	...set $piece(name,%1,7)="L"
	..if HL7("VER")="2.1",msgtype["A19" do
	...set name=$get(NextOfKin(44))_$select($get(NextOfKin(45))'="":" "_NextOfKin(45),1:"")_" "_$get(NextOfKin(43))
	...set $piece(name,%1,6)=name
	..set email=$get(NextOfKin(40)),phoneM=$get(NextOfKin(42)),phoneH=$get(NextOfKin(47)),phoneW=$get(NextOfKin(48))
 
	.set mtable="PA_Nok" do CodeTable^HL7OutExtract1A(mtable)
	.set relation=$get(NextOfKin(8)),contact=$get(NextOfKin(30))
	.set datefrom=$select($get(NextOfKin(28))'="":$$Date^HL7(NextOfKin(28)),1:""),dateto=$select($get(NextOfKin(29))'="":$$Date^HL7(NextOfKin(29)),1:"")
	.set firstcontact=$get(NextOfKin(39)),firstcontact=$select(firstcontact="Y":"FC",1:"")
	.if $get(sex)="" set sex=$get(NextOfKin(53))
	.if HL7("VER")="2.1",msgtype["A19" set contact=""
 
	. // setup contact fields
	.set (homephone,workphone)="" do
	..if msgtype["A19" do  quit
	...if HL7("VER")="2.4" do
	....if $length(phoneW) set workphone=phoneW_%1_"WPN"_%1_%1_%1_%1_%1_phoneW
	....if $length(phoneH) set homephone=phoneH_%1_"PRN"_%1_%1_%1_%1_%1_phoneH
	...if HL7("VER")="2.1" do
	....if $length(phoneW) set workphone=phoneW
	....if $length(phoneH) set homephone=phoneH
	..if $length(phoneW) set workphone=workphone_$select($length(workphone):"~",1:"")_phoneW_%1_"WPN"_%1_"PH"_%1_%1_%1_%1_%1_%1_"P"
	..if $length(phoneH) set homephone=homephone_$select($length(homephone):"~",1:"")_phoneH_%1_"PRN"_%1_"PH"_%1_%1_%1_%1_%1_%1_"P"
	..if $length(phoneM) set homephone=homephone_$select($length(homephone):"~",1:"")_phoneM_%1_"ORN"_%1_"CP"_%1_%1_%1_%1_%1_%1_"P"
	..if $length(email) set homephone=homephone_$select($length(homephone):"~",1:"")_%1_"NET"_%1_"Internet"_%1_email
	..set phtype="" for  set phtype=$order(phone(phtype)) quit:phtype=""  do
	...set phcnt="" for  set phcnt=$order(phone(phtype,phcnt)) quit:phcnt=""  do
	....set homephone=homephone_$select($length(homephone):"~",1:"")_phone(phtype,phcnt)_%1_"PRN"_%1_"PH"_%1_%1_%1_%1_%1_%1_phtype
 
	. // setup permanent/temp address
	.kill Address
	.if $data(PermAddress) do
	..for k="CTY","PRV","PST","FOR-COU" set PermAddress(k)=$piece($$Address^HL7Common7(.reject,"O",$select(k="FOR-COU":"COU",1:k),$get(PermAddress(k))),"|",$select(k="CTY":2,1:1))
	..set Address=$get(PermAddress("LINE1"))_%1_$get(PermAddress("LINE2"))_%1_$get(PermAddress("CTY"))_%1_$get(PermAddress("PRV"))_%1_$get(PermAddress("PST"))_%1_%1_"P"_%1_$get(PermAddress("FOR-CTY"))_%4_$get(PermAddress("FOR-PST"))_%4_$get(PermAddress("FOR-COU"))_%1_%1_%1
	.if $data(TempAddress) do
	..set cnt="" for  set cnt=$order(TempAddress(cnt)) quit:cnt=""  do
	...for k="CTY","PRV","PST","TYP","FOR-COU" set TempAddress(cnt,k)=$piece($$Address^HL7Common7(.reject,"O",$select(k="FOR-COU":"COU",1:k),$get(TempAddress(cnt,k))),"|",$select(k="CTY":2,1:1))
	...set Address=$get(Address)_$select($length(Address):"~",1:"")_$get(TempAddress(cnt,"LINE1"))_%1_$get(TempAddress(cnt,"LINE2"))_%1_$get(TempAddress(cnt,"CTY"))_%1_$get(TempAddress(cnt,"PRV"))_%1_$get(TempAddress(cnt,"PST"))_%1_%1_$get(TempAddress(cnt,"TYP"))_%1_$get(TempAddress(cnt,"FOR-CTY"))_%4_$get(TempAddress(cnt,"FOR-PST"))_%4_$get(TempAddress(cnt,"FOR-COU"))_%1_%1_%1
 
	.set count=count+1
 
	.if count=1 do
	..set DATA("NK1",1)=count,DATA("NK1",2)=name,DATA("NK1",3)=relation,DATA("NK1",4)=Address,DATA("NK1",5)=homephone
	..set DATA("NK1",6)=workphone,DATA("NK1",7)=contact,DATA("NK1",8)=datefrom,DATA("NK1",9)=dateto,DATA("NK1",15)=$piece(sex,"^")
	..set DATA("NK1",29)=firstcontact
	.if count>1,repeat="Y" do
	..set Repeat("NK1",count,1)=count,Repeat("NK1",count,2)=name,Repeat("NK1",count,3)=relation,Repeat("NK1",count,4)=Address
	..set Repeat("NK1",count,5)=homephone,Repeat("NK1",count,6)=workphone,Repeat("NK1",count,7)=contact,Repeat("NK1",count,8)=datefrom
	..set Repeat("NK1",count,9)=dateto,Repeat("NK1",count,15)=$piece(sex,"^"),Repeat("NK1",count,29)=firstcontact
 
	kill PatDetail,NextOfKin,Address,PermAddress,TempAddress,phone
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
	quit 0
 
CheckMRG ;; build DATA array - MRG
MRG()	set mergenum="" kill DATA
 
	do
	.if msgtype["A47" do  quit
	.. // extract patient detail
	..kill PatDetail,PatDetailx set SQLCODE=$$PatDetail^HL7OutExtract1(intRegMrn,.PatDetail,.PatDetailx)
	.. // extract patient number detail
	..kill number set SQLCODE=$$PatNum^HL7OutExtract1(intRegMrn,intAdmNum,.number)
	..set type="HOSP",number(type)=""
	..set intHosp="" for  set intHosp=$order(number(type,intHosp)) quit:intHosp=""  do
	... // s num=number(type,cnt),intHosp=$$intHospRegMrn^HL7Common2($p(num,"^"),intRegMrn)
	... // check if forwarded message
	...if $data(forwardnum(fwdcnt,intHosp)) quit
	...if $get(old("RTMASTER",1))=intHosp quit
	...set active=$$ActiveHospMrn^HL7OutExtract1(intHosp)
	...set data=$piece(num,"^",6),$piece(data,"&",2)=$select(active="Y":"A",1:"I"),$piece(num,"^",6)=data,number(type,intHosp)=num
	...set number(type)=number(type)_$select($length(number(type)):"~",1:"")_number(type,intHosp) quit
	..do NumTypeDetail^HL7Common2(PatNum("HOSP"),"","",.asstype,.asscode,.idcode,.idmrtype)
	..if $data(fowardnum(fwdcnt,intHosp)) do
	...set hospmrn=$get(fowardnum(fwdcnt,intHosp,"RTMASTER-OLD",2)),active=$get(forwardnum(fwdcnt,intHosp,"RTMASTER-OLD",13))
	...set code="" kill rowid("type") set mrtype=$get(forwardnum(fwdcnt,intHosp,"RTMASTER-OLD",9))
	..if '$data(forwardnum(fwdcnt,intHosp)) do
	...set hospmrn=$get(old("RTMASTER",2)),active=$get(old("RTMASTER",13))
	...set code="" kill rowid("type") set mrtype=$get(old("RTMASTER",9))
	..if idmrtype'="Y",mrtype'="" set code=$piece($get(^RTC("TYPE",mrtype)),"^",1)
	..if idmrtype="Y" set code=$$MedRecType^HL7OutExtract1(mrtype)
	..set num=hospmrn,$piece(num,"^",asstype)=code,$piece(num,"^",5)=idcode,$piece(num,"^",6)=$piece($piece(num,"^",6),"&")_"&"_$select(active="Y":"A",active="D":"D",1:"I")
	..set mergenum=number(type)_$select($length(number(type)):"~",1:"")_num
 
	.if msgtype["A40" do  quit
	..if $length($get(intRegFrom)) do
	...do NumTypeDetail^HL7Common2(PatNum("REG"),"","",.asstype,.asscode,.idcode)
	...set mergenum=$$extRegMrn^HL7Common2(intRegFrom)
	...set $piece(mergenum,"^",asstype)=asscode,$piece(mergenum,"^",5)=idcode
	... // extract mrn's moved
	...set intMergeNum=$order(^PAMRi("PAT-FROM",intRegFrom,"")) if intMergeNum'="" do
	....set row="" for  set row=$order(^PAMR(intMergeNum,"DET",row)) quit:row=""  if $piece(^PAMR(intMergeNum,"DET",row),"^")="RTMAS" do
	.....set intHospFrom=$piece($piece(^PAMR(intMergeNum,"DET",row),"^",2),":") if intHospFrom="" quit
	.....set active=$piece($piece(^PAMR(intMergeNum,"DET",row),"^",2),":",2)
	.....if '$data(^RT(intHospFrom)) quit
	.....do NumTypeDetail^HL7Common2(PatNum("HOSP"),"","",.asstype,.asscode,.idcode,.idmrtype)
	.....do MedRecord^HL7OutExtract1(intHospFrom)
	.....set num=$piece(^RT(intHospFrom),"^"),$piece(num,"^",asstype)=code,$piece(num,"^",5)=idcode,$piece(num,"^",6)=$piece($piece(num,"^",6),"&")_"&"_$select(active="Y":"A",active="D":"D",1:"I")
	.....set mergenum=mergenum_$select($length(mergenum):"~",1:"")_num
 
	.if $length($get(intRegFrom)) do
	..do NumTypeDetail^HL7Common2(PatNum("REG"),"","",.asstype,.asscode,.idcode)
	..set mergenum=$$extRegMrn^HL7Common2(intRegFrom)
	..set $piece(mergenum,"^",asstype)=asscode,$piece(mergenum,"^",5)=idcode
	.if $length($get(intHospFrom)) do
	..if '$data(^RT(intHospFrom)) quit
	..if $data(old("RTMASTER")) set active=$get(old("RTMASTER",13))
	..if '$data(old("RTMASTER")) set active=$$ActiveHospMrn^HL7OutExtract1(intHospFrom)
	..do NumTypeDetail^HL7Common2(PatNum("HOSP"),"","",.asstype,.asscode,.idcode,.idmrtype)
	..do MedRecord^HL7OutExtract1(intHospFrom)
	..set num=$piece(^RT(intHospFrom),"^"),$piece(num,"^",asstype)=code,$piece(num,"^",5)=idcode,$piece(num,"^",6)=$piece($piece(num,"^",6),"&")_"&"_$select(active="Y":"A",active="D":"D",1:"I")
	..set mergenum=num
 
	set DATA("MRG",1)=mergenum
 
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
	quit 0
 
CheckIAM ;; build DATA array - IAM
IAM()	set intRegMrn=$get(intRegMrn),(algtext,count)="" kill DATA,Repeat
 
	if trigger'="Delete" do  if none quit 0
	.set none="",check=1
	.if $order(^PAPER(intRegMrn,"ALG",0))="",$order(^PAPER(intRegMrn,"ALM",0))="" set check=""
	.if mandatory'="Y",'check set none=1
	.if mandatory="Y",'check set DATA("IAM",1)=1
 
	// extract allergies
	set seq=0 for  set seq=$order(^PAPER(intRegMrn,"ALG",seq)) quit:seq=""  do  if count,repeat'="Y" quit
	. // extract allergy detail
	.set intAlgNum=intRegMrn_"||"_seq if trigger="Delete",$piece($get(old("PAALLERGY",1)),$char(1))=intAlgNum quit
	.kill Allergy if $$Allergy^HL7OutExtract9(intAlgNum,.Allergy) quit
 
	. // check for new null fields and change to ""
	.if trigger="Update",$get(old("PAALLERGY",1))=intAlgNum do NullChange^HL7Common3("PAALLERGY")
	.do Allergy
 
	if trigger="Delete",$data(old("PAALLERGY")) do
	.kill Allergy merge Allergy=old("PAALLERGY") set intAlgNum=$get(old("PAALLERGY",1))
	.do Allergy
 
	// extract alerts
	set seq=0 for  set seq=$order(^PAPER(intRegMrn,"ALM",seq)) quit:seq=""  do  if count,repeat'="Y" quit
	. // extract alert detail
	.set intAltNum=intRegMrn_"||"_seq if trigger="Delete",$piece($get(old("PAALERTMSG",1)),$char(1))=intAltNum quit
	.kill Alert if $$Alert^HL7OutExtract9(intAltNum,.Alert) quit
 
	. // check for new null fields and change to ""
	.if trigger="Update",$get(old("PAALERTMSG",1))=intAltNum do NullChange^HL7Common3("PAALERTMSG")
	.do Alert
 
	if trigger="Delete",$data(old("PAALERTMSG")) do
	.kill Alert merge Alert=old("PAALERTMSG") set intAltNum=old("PAALERTMSG",1)
	.do Alert
 
	kill Allergy,Alert
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
	quit 0
 
	// build alert detail
Alert
	// extract code table detail
	set mtable="PA_AlertMsg" do CodeTable^HL7OutExtract9(mtable)
	
	// ab 14.07.05 - 53962 - assigning authority is now consistantly sent in the 3rd piece
	for i=21,32,24 if $get(Alert(i))="" set Alert(i)="^"
	for i=21,32,24 if $get(Alert(i))="""""" set Alert(i)=$get(Alert(i))_"^"
 
	set comment="" for i=1:1:$listlength($get(Alert(4))) set comment=comment_$select($length(comment):HL7("LINEBK"),1:"")_$translate($$TextConversion^HL7Common12($list($get(Alert(4)),i),"O"),$char(13,10),HL7("LINEBK"))
	set onsetdate=$select($get(Alert(25))'="":$$Date^HL7(Alert(25)),1:""),status=$get(Alert(30)),uniqueid=$get(Alert(31)),expreason=$get(Alert(24))
	set alerttype=$get(Alert(32)),alertcode=$get(Alert(21))
	
	// action type
	set actioncode=$select(trigger="Insert":"A",trigger="Update":"U",trigger="Delete":"D",1:"U")
	if trigger="Insert",$get(curr("PAALERTMSG",1))'=intAltNum set actioncode="U"
	if trigger="Delete",$get(old("PAALERTMSG",1))'=intAltNum set actioncode="U"
 
	set count=count+1
	// include assigning authority of TRAK [Log 51317]
	if count=1 do
	.set DATA("IAM",1)=count,DATA("IAM",2)=alerttype_"^TRAK",DATA("IAM",3)=alertcode_"^TRAK",DATA("IAM",6)=actioncode
	.set DATA("IAM",7)=uniqueid_"^TRAK",DATA("IAM",8)=expreason_"^TRAK",DATA("IAM",10)=comment,DATA("IAM",11)=onsetdate,DATA("IAM",17)=$piece(status,"^")_"^"_$piece(status,"^",2)_"^TRAK"
	if count>1,repeat="Y" do
	.set Repeat("IAM",count,1)=count,Repeat("IAM",count,2)=alerttype_"^TRAK",Repeat("IAM",count,3)=alertcode_"^TRAK",Repeat("IAM",count,6)=actioncode
	.set Repeat("IAM",count,7)=uniqueid_"^TRAK",Repeat("IAM",count,8)=expreason_"^TRAK",Repeat("IAM",count,10)=comment,Repeat("IAM",count,11)=onsetdate,Repeat("IAM",count,17)=$piece(status,"^")_"^"_$piece(status,"^",2)_"^TRAK"
	quit
 
	// build allergy detail
Allergy	
	// extract code table detail
	set mtable="PA_Allergy" do CodeTable^HL7OutExtract9(mtable)
	// ab 14.07.05 - 53962 - assigning authority is now consistently sent in the 3rd piece
	for i=24,28,31,32,33 do
	.if $get(Allergy(i))="" set Allergy(i)="^"
	.if $get(Allergy(i))="""""" set Allergy(i)=$get(Allergy(i))_"^"
 
	set allergycode="" do  if allergycode="" set allergycode="^"
	.if $get(Allergy(5))'="" set allergycode=Allergy(5) quit
	.if $get(Allergy(8))'="" set allergycode=Allergy(8) quit
	.if $get(Allergy(31))'="" set allergycode=Allergy(31)
 
	set status=$get(Allergy(11)),severity=$select($get(Allergy(24))'="":$get(Allergy(24)),1:"^"),reaction=$select($get(Allergy(28))'="":$get(Allergy(28)),1:"^")
	set onsetdate=$select($get(Allergy(13))'="":$$Date^HL7(Allergy(13)),1:""),allergytype=$select($get(Allergy(33))="":"^",1:$get(Allergy(33)))
	set uniqueid=$get(Allergy(34)),reasonchange=$get(Allergy(32))
	// action type
	set actioncode=$select(trigger="Insert":"A",trigger="Update":"U",trigger="Delete":"D",1:"U")
	if trigger="Insert",$get(curr("PAALLERGY",1))'=intAlgNum set actioncode="U"
	if trigger="Delete",$get(old("PAALLERGY",1))'=intAlgNum set actioncode="U"
	set comment="" for i=1:1:$listlength($get(Allergy(30))) set comment=comment_$select($length(comment):HL7("LINEBK"),1:"")_$translate($$TextConversion^HL7Common12($list($get(Allergy(30)),i),"O"),$char(13,10),HL7("LINEBK"))
 
	set count=count+1
	// include assigning authority of TRAK [Log 51317]
	if count=1 do
	.set DATA("IAM",1)=count,DATA("IAM",2)=allergytype_"^TRAK",DATA("IAM",3)=allergycode_"^TRAK",DATA("IAM",4)=severity_"^TRAK",DATA("IAM",5)=reaction_"^TRAK"
	.set DATA("IAM",6)=actioncode,DATA("IAM",7)=uniqueid_"^TRAK",DATA("IAM",8)=reasonchange_"^TRAK",DATA("IAM",10)=comment,DATA("IAM",11)=onsetdate,DATA("IAM",17)=$piece(status,"^")_"^"_$piece(status,"^",2)_"^TRAK"
	if count>1,repeat="Y" do
	.set Repeat("IAM",count,1)=count,Repeat("IAM",count,2)=allergytype_"^TRAK",Repeat("IAM",count,3)=allergycode_"^TRAK",Repeat("IAM",count,4)=severity_"^TRAK"
	.set Repeat("IAM",count,5)=reaction_"^TRAK",Repeat("IAM",count,6)=actioncode,Repeat("IAM",count,7)=uniqueid_"^TRAK",Repeat("IAM",count,8)=reasonchange_"^TRAK",Repeat("IAM",count,10)=comment,Repeat("IAM",count,11)=onsetdate
	.set Repeat("IAM",count,17)=$piece(status,"^")_"^"_$piece(status,"^",2)_"^TRAK"
	quit



