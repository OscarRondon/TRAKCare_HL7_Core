^MAC^Save for Source Control^^~Format=IRIS.S~^RAW
%RO
HL7SiteQLDCreate2^MAC^^^0
HL7SiteQLDCreate2 // ML 14/07/2003 ; HL7 interface : create transmission packets - Queensland Health
SrcVer ;; $Id: //trak/main/releases/T2020/1/CLXX/rtn/hl7/HL7SiteQLDCreate2.rtn#1 $
	quit
 
CheckPV1 ;; build DATA array - PV1
PV1()	set intRegMrn=$get(intRegMrn),intAdmNum=$get(intAdmNum),SQLCODE="" kill DATA
 
	// build non-admission related PV1
	if msgtype["A31" do
	. // extract current inpatient rowid
	.set (intAdmNum,found)=""
	.set intNum="" for  set intNum=$order(^PAPERdr(intRegMrn,"ADM","I",intNum),-1) quit:intNum=""  do  if current="Y" quit
	..set current=""
	.. &SQL(SELECT PAADM_Current INTO :current FROM PA_Adm WHERE PAADM_RowId = :intNum)
	..if current="Y"	set intAdmNum=intNum
 
	// extract admission detail
	kill AdmDetail if intAdmNum'="" set SQLCODE=$$AdmDetail^HL7OutExtract2(intAdmNum,.AdmDetail)
	if intAdmNum=""!SQLCODE do  quit $select(msgtype["A31":0,msgtype["A28":0,1:SQLCODE)
	.if mandatory'="Y" quit
	.if mandatory="Y" set DATA("PV1",1)=$increment(count("PV1"))
	.merge ^TMP("HL7-OUT",$job)=DATA kill DATA
 
	// check for new null fields and change to ""
	if $data(old("PAADM")) do NullChange^HL7Common3("PAADM")
 
	// extract admission transaction detail
	kill AdmTransaction do
	.set AdmTransRow=""
	.if ",A02,A12,"[(","_$piece(msgtype,"^",2)_",") set intAdmTransNum=intNum
	.if ",A02,A12,"'[(","_$piece(msgtype,"^",2)_",") set intAdmTransNum=intAdmNum_"||"_$order(^PAADM(intAdmNum,"TRANS",""),-1)
	.if $piece(intAdmTransNum,"||",2)'="" set SQLCODE=$$AdmTransaction^HL7OutExtract2(intAdmTransNum,.AdmTransaction)
 
	// extract admission insurance detail
	kill AdmInsurance set intAdmIns=""
	if table="PAADMINSURANCE" set intAdmIns=intNum
	else  set intAdmIns=intAdmNum_"||"_$order(^PAADM(intAdmNum,"INS",""),-1)
	if $piece(intAdmIns,"||",2)'="" do
	.if $$AdmInsurance^HL7OutExtract2(intAdmIns,.AdmInsurance)
	// rank must be one to extract insurance type
	if $get(AdmInsurance(29))'="1" do
	.set AdmInsurance(3)=""
	. // search for insurance type
	.set row=0 for  set row=$order(^PAADM(intAdmNum,"INS",row)) quit:row=""  do
	..if $piece(^PAADM(intAdmNum,"INS",row),"^",27)'=1 quit
	..set AdmInsurance(3)=$piece(^PAADM(intAdmNum,"INS",row),"^")
	..set AdmInsurance(12)=$piece(^PAADM(intAdmNum,"INS",row),"^",10)
 
	// extract code table detail
	for mtable="PA_Adm","PA_AdmInsurance" do CodeTable^HL7OutExtract2A(mtable)
	if table="PAADMTRANSACTION" set mtable="PA_AdmTransaction" do CodeTable^HL7OutExtract2A(mtable)
 
	set admnum=AdmDetail(2),admtype=AdmDetail(9),source=AdmDetail(10),status=AdmDetail(25),preadmnum=AdmDetail(76),mradm=AdmDetail(152),inpattype=$piece($get(AdmDetail(273)),"^")
	set (ward,room,bed,hospital)=""
	if table'="PAADMTRANSACTION" set room=$get(AdmDetail(163)),ward=$get(AdmDetail(164)),bed=$get(AdmDetail(167))
	if table="PAADMTRANSACTION" set room=$get(AdmTransaction(9)),ward=$get(AdmTransaction(10)),bed=$get(AdmTransaction(11))
	set auxilinstype=$piece($get(AdmInsurance(12)),"^")_"&"_$piece($get(AdmInsurance(12)),"^",2),insurancetype=$get(AdmInsurance(3))
	set (admdatetime,dischdatetime)=""
	if $$AdmDates^HL7OutExtract2(intAdmNum,AdmDetail(7),AdmDetail(8),AdmDetail(22),AdmDetail(23),"","",.admdatetime,.dischdatetime)
 
	// extract doctors
	kill doctor set drcodes=""
	for i=12,174,277 if $get(AdmDetail(i))'="" do
	.if AdmDetail(i)=""""!(AdmDetail(i)="""""") quit
	.set type=$select(i=174:"R",1:"C"),drid=AdmDetail(i)
	.set drcode=$$Provider^HL7Common5(drid,"","","",type,"O")
	.set AdmDetail(i)=""
	.for j=1:1:$length(drcode,$char(1)) set dr=$piece(drcode,$char(1),j) do
	..set drcodetype=$piece(dr,$char(2),7)
	..set AdmDetail(i)=AdmDetail(i)_$select($length(AdmDetail(i)):"~",1:"")_$piece(dr,$char(2),1)_"^"_$piece(dr,$char(2),2)_"^"_$piece(dr,$char(2),3)_"^^^^^^"_$select(drcodetype="P":"AUSHIC",1:$piece(dr,$char(2),4))_"^^^^"_$select(drcodetype="D":"DN",drcodetype="P":"PRN",1:"")
	for i=12,174,277 set doctor($select(i=12:"ATTEND",i=174:"REFER",1:"TREAT"))=AdmDetail(i)
	// extract admitting dr
	set (drid,doctor("ADMIT"))=""
	set trans=0 for  set trans=$order(^PAADM(intAdmNum,"TRANS",trans)) quit:trans=""  do  quit:drid'=""
	.if $piece(^PAADM(intAdmNum,"TRANS",trans),"^",5)="" quit
	.set drid=$piece(^PAADM(intAdmNum,"TRANS",trans),"^",5)
	.set drcode=$$Provider^HL7Common5(drid,"","","","C","O")
	.for j=1:1:$length(drcode,$char(1)) set dr=$piece(drcode,$char(1),j) do
	..set drcodetype=$piece(dr,$char(2),7)
	..set doctor("ADMIT")=doctor("ADMIT")_$select($length(doctor("ADMIT")):"~",1:"")_$piece(dr,$char(2),1)_"^"_$piece(dr,$char(2),2)_"^"_$piece(dr,$char(2),3)_"^^^^^^"_$select(drcodetype="P":"AUSHIC",1:$piece(dr,$char(2),4))_"^^^^"_$select(drcodetype="D":"DN",drcodetype="P":"PRN",1:"")
 
	// med record detail : ambulatory status, discharge condition, discharged to location
	kill MRAdm if mradm'="" do
	.if $$MRAdmDetail^HL7OutExtract4(mradm,.MRAdm) quit
	. // check for new null fields and change to ""
	.if $data(old("MRADM")) do NullChange^HL7Common3("MRADM")
	. // extract code table detail
	.set mtable="MR_Adm" do CodeTable^HL7OutExtract4(mtable)
	set dischtype=$get(MRAdm(15)),ambul=$get(MRAdm(44)),dischloc=$piece($get(MRAdm(55)),"^")_"^"_$select($piece($get(MRAdm(55)),"^",3)="":"",1:$$Date^HL7($piece($get(MRAdm(55)),"^",3)))
	set caretype=$piece($get(MRAdm(63)),"^")
 
	// extract admission creation facility
	set CreationFacility="",auditdate=$order(^AUD(0,"TableRow","PAADM",intAdmNum,"")) if auditdate'="" do
	.set row="" for  set row=$order(^AUD(0,"TableRow","PAADM",intAdmNum,auditdate,row)) quit:row=""  do  quit:action="I"
	..set action=$piece(^AUD(row),"^",3) if action'="I" quit
	..set auditloc=$piece(^AUD(row),"^",9)
	.. &SQL(SELECT HOSP_Code INTO :CreationFacility FROM CT_Loc, CT_Hospital WHERE CTLOC_RowId = :auditloc AND CTLOC_Hospital_DR = HOSP_RowId)
	..set CreationFacility=$$TextConversion^HL7Common12(CreationFacility,"O")
 
	// adjust location
	if admtype'="O" set DATA("PV1",3)=$select(ward'="":ward,1:location)_%1_$select(ward'="":room,1:"")_%1_$select(ward'="":bed,1:"")_%1_$get(hospcode)_%1_%1_%1_$get(campuscode)
	if admtype="O" set DATA("PV1",3)=location_%1_%1_%1_$get(hospcode)_%1_%1_%1_$get(campuscode)
 
	set DATA("PV1",1)=$increment(count("PV1")),DATA("PV1",2)=$select(status="P":"P",1:admtype),DATA("PV1",4)=inpattype,DATA("PV1",7)=$get(doctor("ATTEND")),DATA("PV1",8)=$get(doctor("REFER")),DATA("PV1",9)=$get(doctor("TREAT")),DATA("PV1",10)=location
	set DATA("PV1",14)=source,DATA("PV1",15)=ambul,DATA("PV1",17)=$get(doctor("ADMIT")),DATA("PV1",19)=admnum_"^^^MEDTRAK^VN^"_CreationFacility,DATA("PV1",20)=insurancetype
	set DATA("PV1",21)=auxilinstype,DATA("PV1",36)=dischtype,DATA("PV1",37)=dischloc,DATA("PV1",44)=admdatetime,DATA("PV1",45)=dischdatetime
 
	// extract previous location
	if ",A02,A12,"[(","_$piece(msgtype,"^",2)_",") do
	.new ward,room,bed set intAdmTransNum=otherNum kill AdmTransaction
	.if trigger="Insert",otherNum="" do
	.. // transfer
	..if $piece(msgtype,"^",2)="A02" do
	...set found=""
	...set seq=$piece(curr("PAADMTRANSACTION",1),"||",2) for  set seq=$order(^PAADM($piece(curr("PAADMTRANSACTION",1),"||"),"TRANS",seq),-1) quit:seq=""  do  quit:found
	....set detail=$get(^PAADM($piece(curr("PAADMTRANSACTION",1),"||"),"TRANS",seq)),bed=$piece(detail,"^",8),enddatetime=$piece(detail,"^",3,4)
	....if bed="" quit
	....if bed'=$piece($get(curr("PAADMTRANSACTION",11)),$char(1)),$translate(enddatetime,"^")'="" set intAdmTransNum=$piece(curr("PAADMTRANSACTION",1),"||")_"||"_seq,found=1
	.. // cancel transfer
	..if $piece(msgtype,"^",2)="A12" do
	...set (found,start)=""
	...if trigger'="Delete",otherNum="" do
	....set seq=$piece(curr("PAADMTRANSACTION",1),"||",2) for  set seq=$order(^PAADM($piece(curr("PAADMTRANSACTION",1),"||"),"TRANS",seq),-1) quit:seq=""  do  quit:found
	.....set detail=$get(^PAADM($piece(curr("PAADMTRANSACTION",1),"||"),"TRANS",seq)),bed=$piece(detail,"^",8),enddatetime=$piece(detail,"^",3,4)
	.....if bed="" quit
	.....if bed'=$piece($get(curr("PAADMTRANSACTION",11)),$char(1)),$translate(enddatetime,"^")'="" set start=start+1
	.....if 'start quit
	.....if bed=$piece($get(curr("PAADMTRANSACTION",11)),$char(1)),($translate(enddatetime,"^")'=""),start=1 set intAdmTransNum=$piece(curr("PAADMTRANSACTION",1),"||")_"||"_$order(^PAADM($piece(curr("PAADMTRANSACTION",1),"||"),"TRANS",seq)),found=1
	.if trigger="Update",otherNum="" do
	.. // transfer
	..if $piece(msgtype,"^",2)="A02" do  quit
	...set AdmTransaction(9)=$get(old("PAADMTRANSACTION",9)),AdmTransaction(10)=$get(old("PAADMTRANSACTION",10)),AdmTransaction(11)=$get(old("PAADMTRANSACTION",11))
	.. // cancel transfer
	..if $piece(msgtype,"^",2)="A12" do  quit
	...set intAdmTransNum=otherNum
 
	.if $piece(intAdmTransNum,"||",2)'="" do
	..set SQLCODE=$$AdmTransaction^HL7OutExtract2(intAdmTransNum,.AdmTransaction) if SQLCODE quit
	.do WardRoomBed^HL7OutExtract2($get(AdmTransaction(10)),$get(AdmTransaction(9)),$get(AdmTransaction(11)))
	.set DATA("PV1",6)=wardcode_%1_roomcode_%1_bedcode_%1_$get(hospcode)_%1_%1_%1_$get(campuscode)
 
	for i=1:1:$order(DATA("PV1",""),-1) if $translate($get(DATA("PV1",i)),"^&")="" set DATA("PV1",i)=""
 
	kill AdmDetail,AdmInsurance,AdmTransaction,MRAdm,doctor
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
	quit 0
 
CheckPV2 ;; build DATA array - PV2
PV2()	set intAdmNum=$get(intAdmNum),SQLCODE="" kill DATA
 
	// extract admission details
	kill AdmDetail if intAdmNum'="" set SQLCODE=$$AdmDetail^HL7OutExtract2(intAdmNum,.AdmDetail)
	if intAdmNum=""!SQLCODE do  quit SQLCODE
	.if mandatory'="Y" quit
	.if mandatory="Y" set DATA("PV2",1)=""
	.merge ^TMP("HL7-OUT",$job)=DATA kill DATA
 
	// check for new null fields and change to ""
	if $data(old("PAADM")) do NullChange^HL7Common3("PAADM")
 
	// extract code table detail
	set mtable="PA_Adm" do CodeTable^HL7OutExtract2A(mtable)
 
	set visucode=$get(AdmDetail(299)),mradm=$get(AdmDetail(152))
	set estdischdate=$get(AdmDetail(140)),estdischtime=$get(AdmDetail(141))
 
	// extract expected 'leave of absence' return date and time
	set expLOAretdatetime=""
	if $$LeaveOfAbsence^HL7OutExtract2(intAdmNum,.expLOAretdatetime)
 
	// extract operating detail
	set (operdate,opertime)="" kill WaitListAdm
	set intWaitNum=$$WaitListExist^HL7OutExtract3(intAdmNum) if intWaitNum'="" do
	.set row=$order(^PAWLi("PAADM",intAdmNum,intWaitNum,""),-1) if row=""!(row=0) quit
	.set intWaitAdm=intWaitNum_"||"_row
	.if $$WaitListAdm^HL7OutExtract3(intWaitAdm,.WaitListAdm) quit
	set operdate=$get(WaitListAdm(16)),opertime=$get(WaitListAdm(17))
 
	// med record admission detail
	kill MRAdm if mradm'="" do
	.if $$MRAdmDetail^HL7OutExtract4(mradm,.MRAdm) quit
	.set mtable="MR_Adm" do CodeTable^HL7OutExtract4(mtable)
	set publicity=$get(MRAdm(45)),admitreason=$get(MRAdm(85))
 
	// setup date fields
	set DATA("PV2",3)=admitreason,DATA("PV2",7)=visucode,DATA("PV2",21)=publicity
	if $get(estdischdate)'="" set DATA("PV2",9)=$$Date^HL7(estdischdate_","_estdischtime)
	if $get(operdate)'="" set DATA("PV2",33)=$$Date^HL7(operdate_","_opertime)
	set DATA("PV2",47)=expLOAretdatetime
 
	kill AdmDetail,WaitListAdm,MRAdm
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
	quit 0
 
CheckIN1 ;; build DATA array - IN1
IN1()	set intRegMrn=$get(intRegMrn),intAdmNum=$get(intAdmNum),(intAdmIns,found,SQLCODE)="" kill DATA
 
	set tabletype=$$table^HL7OutboundADT(table)
 
	// extract patient insurance detail
	if tabletype'="A" do
	.kill PatDetail,PatDetailx set SQLCODE=$$PatDetail^HL7OutExtract1(intRegMrn,.PatDetail,.PatDetailx)
	.if SQLCODE quit
	.if $get(PatDetail(31))'=""!($get(PatDetail(142))'="")!($get(PatDetailx(119))'="") set found=1
 
	// select admission insurance detail
	if tabletype="A" do
	.if table="PAADMINSURANCE" set intAdmIns=intNum
	.else  set intAdmIns=intAdmNum_"||"_$order(^PAADM(intAdmNum,"INS",""),-1)
	.if $piece(intAdmIns,"||",2)="" quit
	.kill AdmInsurance set SQLCODE=$$AdmInsurance^HL7OutExtract2(intAdmIns,.AdmInsurance)
	.if $data(AdmInsurance) set found=1
 
	set InsCategory="" do
	.set rowid=$select(tabletype'="A":$piece($get(PatDetail(31)),$char(1)),1:$piece($get(AdmInsurance(3)),$char(1)))
	. &SQL(SELECT INST_Category INTO :InsCategory FROM ARC_InsuranceType WHERE INST_RowId = :rowid)
	.set InsCategory=$$TextConversion^HL7Common12(InsCategory,"O")
	if InsCategory'="Private" set found=""
 
	// check for new null fields and change to ""
	for xtable="PAPER" if $data(old(xtable)) do
	.do NullChange^HL7Common3(xtable)
 
	if 'found do
	.if mandatory'="Y" quit
	.if mandatory="Y" set DATA("IN1",1)="1"
 
	if found do
	. // extract code table detail
	.if $data(AdmInsurance) set mtable="PA_AdmInsurance" do CodeTable^HL7OutExtract2A(mtable)
	.if $data(PatDetail) set mtable="PA_Person" do CodeTable^HL7OutExtract1A(mtable)
	.set (insurer,plan,policynum)=""
	.if tabletype="A" do
	..set insurer=$get(AdmInsurance(3)),plan=$get(AdmInsurance(12)),policynum=$get(AdmInsurance(8))
	.if tabletype'="A" do
	..set insurer=$get(PatDetail(31)),plan=$get(PatDetail(142)),policynum=$get(PatDetailx(119))
	.set DATA("IN1",1)=1,DATA("IN1",2)=$piece(plan,"^")_%1_$piece(plan,"^",2),DATA("IN1",3)=$piece(insurer,"^"),DATA("IN1",4)=$piece(insurer,"^",2)
	.set DATA("IN1",36)=policynum
	
	kill AdmInsurance,PatDetail,PatDetailx
	merge ^TMP("HL7-OUT",$job)=DATA kill DATA
	quit 0



