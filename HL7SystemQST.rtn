^MAC^Save for Source Control^^~Format=IRIS.S~^UTF8
%RO
HL7SystemQST^MAC^^^0
HL7SystemQST // ML 10/10/2003 ; HL7 interface - result processing for Quest Diagnostics
SrcVer ;; $Id: //trak/main/releases/T2020/1/CLXX/rtn/hl7/HL7SystemQST.rtn#1 $
	quit
	
	// results in data array
File(intOrdNum,TextType,TestSegment,ResultType,TestStatus,DelAtomicRes,reject)
	set intOrdNum=$get(intOrdNum),TextType=$get(TextType),TestSegment=$get(TestSegment),ResultType=$get(ResultType),TestStatus=$get(TestStatus),DelAtomicRes=$get(DelAtomicRes)
	set ord1=$piece(intOrdNum,"||"),ord2=$piece(intOrdNum,"||",2),updated="Y"
	set (NewRes,reject)=""
	kill TEST
	
	// convert test code
	set $piece(TestSegment,"^",1)=##class(%Collation).AlphaUp($piece(TestSegment,"^",1))
	
	// check if non-Labtrak system
	set intextMrn=RegMrn if $$LabSystem^HL7Common2()="Y" set intextMrn=intRegMrn
	
	// process results
	do Image
	
	// file test comments against order item
	if $data(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR-NTE")) do
	. do OrderComments^HL7Common11(intOrdNum,cnt1,cnt2,cnt3)
	
	// mark order as read/clear date, time, user read detail
	if updated="Y" do OrderUpdate^HL7Common3(intOrdNum,$select(HL7("MARKREAD")="Y":"RD",1:"UR"),$select('NewRes:HL7("USER"),1:""))
	
	// update status of order to 'executed'
	do ChangeStatus^COEOrdItem(intOrdNum,"E",HL7("USER"))
	
	// 'ok' to continue
	set SQLCODE=0
	quit
	
	// process 'image' results
Image
	// extract authorised date and time
	set datetime=$piece(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",22),"^")
	set authdate=$extract(datetime,1,8),authdate=$select(authdate?8N:$$DateConv^HL7Common2(.reject,"EI",authdate),1:"")
	set authtime=$extract(datetime,9,12),authtime=$select(authtime?4N:$$TimeConv^HL7Common2("EI",$extract(authtime,1,2)_":"_$extract(authtime,3,4)),1:0)
	
	if SubCatType="LAB" do
	. // add/update specimen on order
	. set specimen=$piece(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",15),"^") if $piece(specimen,"&")'="" do
	. . set sitecode=$piece(^TMP("HL7",$job,cnt1,cnt2,parent,"OBR",15),"^",4)
	. . do OrderSpec^HL7Common9(intOrdNum,$piece(specimen,"&"),$piece(sitecode,"&"),$get(date("COL")),$get(time("COL")))
	
	kill Result
	// find sections to post into
	set TextSection=$$TextSection^HL7Common3(TextType)
	set Result(TextSection,$increment(Result(TextSection)))=""
	
	// set flag
	set flag=""
	
	// build the text array
	set cnt4="" for  set cnt4=$order(^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4)) quit:cnt4=""  do
	. if '$data(^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX")) quit
	. // exclude non-reportable result
	. if $get(^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX",5,1))="DNR" quit
	. set line="",type=^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX",2)
	. set line=$extract($piece(^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX",3),%1,2)_"                    ",1,20)
	. // numeric result
	. if type="NM" do  quit
	. . set line=line_" "_$justify(^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX",5,1),12)_" "_$extract(^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX",6)_"              ",1,14)
	. . if ^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX",7)'="" set line=line_" : "_$justify(^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX",7),4)
	. . if ^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX",8)'="" set line=line_" : "_^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX",8)
	. if type="CE" set line=line_" "_$piece(^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX",5,1),"^",2)
	. if type'="ST" set Result(TextSection,$increment(Result(TextSection)))=line
	. if type="ST" do
	. . set i="" for  set i=$order(^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX",5,i)) quit:i=""  set line=line_" "_^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX",5,i),Result(TextSection,$increment(Result(TextSection)))=line,line=""
	. set i="" for  set i=$order(^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX-NTE",3,i)) quit:i=""  set Result(TextSection,$increment(Result(TextSection)))=^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX-NTE",3,i)
	. if flag=""!(flag="N") set flag=$get(^TMP("HL7",$job,cnt1,cnt2,cnt3,cnt4,"OBX",8))
	
	// convert formatting codes
	set x="" for  set x=$order(Result(TextSection,x)) quit:x=""  do
	. set Result(TextSection,x)=$$TextFormatCode^HL7Common12($zname,Result(TextSection,x))
	
	// result status : F = Final result (status 'V'erified)
	//                 C = Corrected result (status 'C'orrected)
	//                 * = All other results (status 'E'ntered)
	set status=$select(TestStatus="C":"C",TestStatus="F":"V",1:"E")
	
	// check if result exists
	set TextId=$order(^OEORD(ord1,"I",ord2,"TR",0)),exists=1 if TextId'="" set TextId=$get(^OEORD(ord1,"I",ord2,"TR",TextId))
	kill PLIST if $$SelImageResult^HL7ResultImage(TextId) do
	. set exists="" for j=2:1:15 set PLIST(j)=""
	
	// file results
	for j=2:1:12 set PLIST(j)=""
	set PLIST(2)=ord1,PLIST(3)=$order(^OEC("RESST",0,"Code",status,"")),PLIST(5)=TextType,PLIST(6)=+$h,PLIST(7)=$piece($h,",",2)
	set PLIST(12)=$piece(TestSegment,%1),PLIST(9)=authdate,PLIST(10)=authtime,PLIST(23)=^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",25)
	if status="V"!(status="C") set PLIST(11)=HL7("USER"),PLIST(4)=$piece($get(^SSU("SSUSR",HL7("USER"))),"^",14)
	set PLIST(30)=flag
	// mark as read/clear date, time and user read fields
	if HL7("MARKREAD")'="Y" set (PLIST(13),PLIST(14),PLIST(15))="",PLIST(25)=HL7("USER"),PLIST(26)=+$h,PLIST(27)=$piece($h,",",2)
	if HL7("MARKREAD")="Y" set PLIST(13)=+$h,PLIST(14)=$piece($h,",",2),PLIST(15)=HL7("USER"),(PLIST(25),PLIST(26),PLIST(27))=""
	// verifying care provider
	set VerifyingDr=$get(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",32))
	set drfound="" for i=1:1:$length(VerifyingDr,"~") do  quit:$piece(drfound,$char(1))'=""
	. set doctor=$piece(VerifyingDr,"^") if doctor="" quit
	. set drsystem=$piece(VerifyingDr,"^",9) if drsystem="TRAKCARE" set drsystem=""
	. set drfound=$$Provider^HL7Common5(doctor,drsystem,"","","C","I")
	set PLIST(34)=$piece(drfound,$char(1))
	// file text result
	do FileImage^HL7ResultImage kill PLIST,Result
	
	// result date/time executed
	set (dateExe,timeExe)=""
	set datetime=$get(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",7)) if datetime'="" do
	. set dateExe=$extract(datetime,1,8),timeExe=$extract(datetime,9,12)
	. if dateExe?8N set dateExe=$$DateConv^HL7Common2(.reject,"EI",dateExe) if dateExe="" quit
	. if timeExe?4N set timeExe=$$TimeConv^HL7Common2("EI",$extract(timeExe,1,2)_":"_$extract(timeExe,3,4))
	. if datetime="""""" set (dateExe,timeExe)=""
	. do OrderUpdate^HL7Common3(intOrdNum,"MF",dateExe_"^"_timeExe,"115^116")
	
	set NewRes=exists
	quit



