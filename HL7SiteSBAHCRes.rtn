^MAC^Save for Source Control^^~Format=IRIS.S~^UTF8
%RO
HL7SiteSBAHCRes^MAC^^^0
HL7SiteSBAHCRes // ML 06/01/2005 ; HL7 interface : result processing - Sultan bin Abdulaziz Humanitarian Centre
SrcVer ;; $Id: //trak/main/releases/T2020/1/CLXX/rtn/hl7/HL7SiteSBAHCRes.rtn#1 $
	quit
	
Check	;; results in data array
Update	set $ztrap="Error^"_$zname
	
	// get patient
	set cnt1=0 for  set cnt1=$order(^TMP("HL7",$job,cnt1)) quit:cnt1=""  do  if SQLCODE quit
	. if ",MSH,MSA,EVN,QRD,QRF,QAK,"[(","_cnt1_",") quit
	. set reject=""
	. if '$data(^TMP("HL7",$job,cnt1,"PID")) do  quit
	. . set reject="Required segment PID not received in message",reject("Detail")="PID^^^~"_100
	. . do Reject^HL7Reject(reject),AckNak^HL7AckNak("AR",reject,$piece(reject("Detail"),"~"),$piece(reject("Detail"),"~",2))
	. // extract internal/external registration numbers
	. set numbers=$$RegNum^HL7Common2(.reject,"R01")
	. set RegMrn=$piece(numbers,"|"),intRegMrn=$piece(numbers,"|",2)
	. if reject'="" do Reject^HL7Reject(reject,"R"),AckNak^HL7AckNak("AR",reject,$piece(reject("Detail"),"~"),$piece(reject("Detail"),"~",2)) quit
	. // check if merged
	. set merge="",masternum=$$patto^CPAPerson(intRegMrn)
	. if masternum'=intRegMrn do
	. . set merge="Y",intRegMrn=masternum,RegMrn=$$extRegMrn^HL7Common2(intRegMrn)
	
	. // get episode
	. set (AdmNum,intAdmNum)=""
	. set cnt2=0 for  set cnt2=$order(^TMP("HL7",$job,cnt1,cnt2)) quit:cnt2=""  do  quit:reject'=""
	. . set AdmNum=$piece($get(^TMP("HL7",$job,cnt1,cnt2,"PV1",19)),"^"),intAdmNum=""
	. . if ##class(%Collation).AlphaUp($piece($get(^TMP("HL7",$job,cnt1,cnt2,"PV1",19)),"^"))="" set AdmNum=""
	
	. . // get order
	. . set (TestStatus,parent)=""
	. . set cnt3=0 for x=0:1 set cnt3=$order(^TMP("HL7",$job,cnt1,cnt2,cnt3)) quit:cnt3=""  do  if SQLCODE quit
	. . . if '$data(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR")) quit
	. . . set (intOrdNum,OrdNum,FillerNum,tests)="" kill date,time,OrdDetail
	. . . // check patient details - special for SBAHC
	. . . if $$PatCheck(intRegMrn,RegMrn,merge) do  quit
	. . . . do Reject^HL7Reject(reject,"R"),AckNak^HL7AckNak("AR",reject,$piece(reject("Detail"),"~"),$piece(reject("Detail"),"~",2))
	
	. . . set reject=""
	
	. . . // check order details
	. . . set numbers=$$OrderNumExtract^HL7Common5(cnt1,cnt2,cnt3)
	. . . set OrdNum=$piece($piece(numbers,"|"),"_"),FillerNum=$piece(numbers,"|",2)
	. . . if OrdNum=""""""!($extract(##class(%Collation).AlphaUp(OrdNum),1,3)="UNK") set OrdNum=""
	. . . do OrdNum^HL7Common2("Y",.reject) if reject'="" do  quit
	. . . . do Reject^HL7Reject(reject,"R"),AckNak^HL7AckNak("AR",reject,$piece($get(reject("Detail")),"~"),$piece($get(reject("Detail")),"~",2))
	
	. . . kill date,time
	. . . // extract required date/time's to check
	. . . set datetime=$get(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",7)),date("COL")=$extract(datetime,1,8),time("COL")=$extract(datetime,9,12)
	. . . for datetype="COL" do
	. . . . if $data(date(datetype)) set date(datetype)=$select(date(datetype)?8N:$$DateConv^HL7Common2(.reject,"EI",date(datetype)),1:"")
	. . . . if $data(time(datetype)) set time(datetype)=$select(time(datetype)?4N:$$TimeConv^HL7Common2("EI",$extract(time(datetype),1,2)_":"_$extract(time(datetype),3,4)),1:"")
	
	. . . // check if admission exists
	. . . do AdmNum^HL7Common2(.reject) if reject'="" do  quit
	. . . . do Reject^HL7Reject(reject,"R"),AckNak^HL7AckNak("AR",reject,$piece($get(reject("Detail")),"~"),$piece($get(reject("Detail")),"~",2))
	
	. . . // unsolicited result - find current admission number
	. . . if AdmNum="none" do  if reject'="" quit
	. . . . do CreateAdm(.reject)
	
	. . . // check for 'incorrect' multiple tests in OBR records, file results against all tests
	. . . for count=1:1:$length(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",4),"~") do
	. . . . set tests=$piece(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",4),"~",count) quit:tests=""
	. . . . set parent=cnt3
	
	. . . . // extract test status
	. . . . set TestStatus=$piece($get(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",25)),"^")
	
	. . . . // check if test sent
	. . . . if tests="" do  quit
	. . . . . set reject="No test sent on order "_OrdNum_", Patient Id "_RegMrn do Reject^HL7Reject(reject,"R"),AckNak^HL7AckNak("AR",reject,"OBR^"_$get(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",1))_"^4",101)
	
	. . . . // check remaining details
	. . . . set (intOrdNum,OrderedItem,OrderMaster)="" kill OrdDetail,res
	. . . . // use placer order number
	. . . . set orddet=$$ExternalCode^HL7Common1(.reject,$piece(tests,"^"),$piece(tests,"^",2))
	. . . . if reject'="" do Reject^HL7Reject(reject,"R"),AckNak^HL7AckNak("AR",reject,"OBR^"_$get(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",1))_"^4",103) quit
	. . . . set OrderedItem=$piece(orddet,"^"),reclocat=$piece(orddet,"^",2)
	
	. . . . set OrderRow=$$CheckLab^HL7Common1(OrdNum,FillerNum,intAdmNum,RegMrn,OrderedItem,"",reclocat,"","",.date,.time)
	. . . . set intOrdNum=$piece(OrderRow,"^"),OrderNew=$piece(OrderRow,"^",2)
	. . . . if intOrdNum="" do  quit
	. . . . . set reject="Unable to add order "_OrdNum_", test "_tests do Reject^HL7Reject(reject,"R")
	
	. . . . // get external info system
	. . . . set ExternalSys=$$ExternalSys^HL7Common1(intOrdNum,.reject)
	. . . . if reject'="" do Reject^HL7Reject(reject,"R"),AckNak^HL7AckNak("AR",reject,"OBR^"_$get(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",1))_"^4",207)
	
	. . . . // get HL7 result type : AT - Atomic
	. . . . //                       IM - Image
	. . . . set ResultType=$$ResultType^HL7Common1(intOrdNum)
	
	. . . . // extract text result type
	. . . . set TextType=""
	. . . . if ResultType="IM" do  quit:reject'=""
	. . . . . set TextType=$$TextType^HL7Common3(intOrdNum)
	. . . . . if TextType="" set reject="No text result type setup against sub-category for image result on test "_$piece(tests,%1)_", order "_OrdNum do Reject^HL7Reject(reject,"R"),AckNak^HL7AckNak("AR",reject,"OBR^"_$get(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",1))_"^4",207)
	
	. . . . // perform result filing
	. . . . set %routine=$$ResultFile^HL7Common1(ExternalSys)
	. . . . if $length(%routine) xecute "d File^"_%routine_"("""_intOrdNum_""","""_TextType_""","""_tests_""","""_ResultType_""","""_TestStatus_""","""",.reject)"
	. . . . if reject'="" quit
	
	. . . . // add filler number to order
	. . . . if FillerNum'="" do OrderUpdate^HL7Common3(intOrdNum,"FN",FillerNum)
	
	. . . . // update order with updated fields
	. . . . if $data(OrdDetail) do OrderUpdate^HL7Common3(intOrdNum,"","","",1) kill OrdDetail
	
	. . if x=0,'SQLCODE do AckNak^HL7AckNak("AR","No results sent for Patient Id "_RegMrn,"OBR^"_$get(^TMP("HL7",$job,cnt1,cnt2,cnt3,"OBR",1))_"^4",100)
	
	// unlock all  - unable to unlock OEORD by itself
	zdeallocate
	
	if 'SQLCODE do AckNak^HL7AckNak("AA")
	quit
	
CheckAdm	;; unsolicited result - find current admission number
CreateAdm(reject) set (found,intAdmNum,AdmNum)=""
	
	// check 'I'npat and 'E'mergency episodes first
	do
	. for admtype="I","E" kill ^TMP($zname,$job,"Find-Adm",intRegMrn) do  quit:found
	. . set intnum=0 for  set intnum=$order(^PAPERdr(intRegMrn,"ADM",admtype,intnum)) quit:intnum=""  do
	. . . kill PAADM if $$SelectAdm(intnum) quit
	. . . // do not include pre-admissions
	. . . quit:$get(PAADM(25))="P"
	. . . set admdate=$piece(PAADM(7),$char(1)),admtime=$piece(PAADM(8),$char(1)) if admtime="" set admtime=" "
	. . . set dischdate=$piece($get(PAADM(22)),$char(1)),dischtime=$piece($get(PAADM(23)),$char(1))
	. . . quit:admdate=""  set ^TMP($zname,$job,"Find-Adm",intRegMrn,admdate,admtime)=intnum_"^"_$get(PAADM(2))_"^"_dischdate_"^"_dischtime
	
	. . set admdate="" for  set admdate=$order(^TMP($zname,$job,"Find-Adm",intRegMrn,admdate),-1) quit:admdate=""  do  quit:found
	. . . set admtime="" for  set admtime=$order(^TMP($zname,$job,"Find-Adm",intRegMrn,admdate,admtime),-1) quit:admtime=""  do  quit:found
	. . . . set x=^TMP($zname,$job,"Find-Adm",intRegMrn,admdate,admtime),intnum=$piece(x,"^"),dischdate=$piece(x,"^",3),dischtime=$piece(x,"^",4)
	. . . . if dischdate="" do
	. . . . . if date("COL")>(admdate-1) do
	. . . . . . if date("COL")=admdate do  quit
	. . . . . . . if time("COL")=""!(time("COL")>(admtime-1)) set found=1
	. . . . . . set found=1
	. . . . if dischdate'="" do
	. . . . . if date("COL")>(admdate-1),(date("COL")<(dischdate+1)) do
	. . . . . . if time("COL")="" set found=1
	. . . . . . if date("COL")=admdate,(date("COL")=dischdate) do  quit
	. . . . . . . if time("COL")>(admtime-1),((time("COL")<($select(dischtime'="":dischtime+1,1:86400)))) set found=1
	. . . . . . if date("COL")>admdate,(date("COL")<dischdate) set found=1
	. . . . . . if date("COL")=admdate,(time("COL")>(admtime-1)) set found=1
	. . . . . . if date("COL")=dischdate,(time("COL")<(dischtime+1)) set found=1
	. . . . if found set AdmNum=$piece(x,"^",2),intAdmNum=intnum
	
	. // check for default episode
	. if 'found do
	. . set intnum="" for  set intnum=$order(^PAADMi("RefDoc",intRegMrn,HL7("DEFREFDR"),intnum),-1) quit:intnum=""  do  quit:found
	. . . kill PAADM if $$SelectAdm(intnum) quit
	. . . quit:$get(PAADM(9))'=HL7("DEFADMTYPE")
	. . . quit:$get(PAADM(5))'=HL7("DEFADMDEPT")
	. . . set found=1,intAdmNum=intnum,AdmNum=$get(PAADM(2))
	
	. . // create default admission
	. . if 'found do
	. . . set AdmDate=+$h,AdmTime=$piece($h,",",2),HL7Flag="Y"
	. . . set intAdmNum=$$ADMInsert^CPAAdm(intRegMrn,AdmDate,AdmTime,"",HL7("DEFADMDEPT"),HL7("DEFREFDR"),"","","","","","","",HL7("DEFADMTYPE"),"","","","","")
	. . . kill HL7Flag
	. . . if intAdmNum="" set reject="Unable to create/locate admission episode for unsolicited result order "_OrdNum_", Patient Id "_RegMrn do Reject^HL7Reject(reject,"R"),AckNak^HL7AckNak("AR",reject,"",207)
	. . . set AdmNum=$piece(^PAADM(intAdmNum),"^",81)
	
	kill ^TMP($zname,$job,"Find-Adm",intRegMrn)
	quit
	
	// select admission details
SelectAdm(intnum) set intnum=$get(intnum)
	&SQL(SELECT * INTO :PAADM() FROM PA_Adm WHERE PAADM_RowID =:intnum)
	quit SQLCODE
	
	// compare patient detail - special for SBAHC
PatCheck(intid,extid,merge) set intid=$get(intid),extid=$get(extid),merge=$get(merge) set (HL7Surname,HL7Given,HL7Middle,SQLCODE)=""
	
	if intid'="",extid="" do
	. &SQL(SELECT %ALPHAUP(PAPMI_Name),%ALPHAUP(PAPMI_Name4),%ALPHAUP(PAPMI_Name3), %INTERNAL(PAPMI_Dob), CTSEX_Code
		INTO :surname, :given, :middle, :dob, :sex FROM PA_PatMas, CT_Sex
		WHERE PAPMI_RowId = :intid AND CTSEX_RowID = PAPMI_Sex_DR)
	if SQLCODE set reject="Unable to extract stored patient details for Patient Id (internal) "_intid,reject("Detail")="^^~207" quit 1
	
	if intid'="",extid'="" do
	. &SQL(SELECT %ALPHAUP(PAPMI_Name),%ALPHAUP(PAPMI_Name4),%ALPHAUP(PAPMI_Name3), %INTERNAL(PAPMI_Dob), CTSEX_Code
		INTO :surname, :given, :middle, :dob, :sex FROM PA_PatMas, CT_Sex
		WHERE PAPMI_No = :extid AND CTSEX_RowID = PAPMI_Sex_DR)
	if SQLCODE set reject="Unable to extract stored patient details for Patient Id (external) "_extid,reject("Detail")="^^~207" quit 1
	// extract current details only - match cannot happen on merged patients as details from msg to actual patient may be different [Log 81320]
	if merge="Y" quit 0
	// check if matching on registration/med record number only
	if $$Rules^HL7Common7(link)
	if $get(RULES(11))="Y" quit 0
	set HL7Surname=$piece(^TMP("HL7",$job,cnt1,"PID",5),%1,1),HL7Given=$piece(^TMP("HL7",$job,cnt1,"PID",5),%1,2),HL7Middle=$piece(^TMP("HL7",$job,cnt1,"PID",5),%1,3)
	// check name
	if ##class(%Collation).AlphaUp($translate($$BuildName^HL7Common3(surname,given,middle),%1))'=##class(%Collation).AlphaUp($translate($$BuildName^HL7Common3(HL7Surname,HL7Given,HL7Middle),%1)) do  quit 1
	. set reject="Name sent for Patient Id "_extid_" does not match system details",reject("Detail")="PID^"_$get(^TMP("HL7",$job,cnt1,"PID",1))_"^5~103"
	// check dob
	if ^TMP("HL7",$job,cnt1,"PID",7)?8N,dob'=$$DateConv^HL7Common2(.reject,"EI",$extract(^TMP("HL7",$job,cnt1,"PID",7),1,8)) do  quit 1
	. set reject="DOB sent for Patient Id "_extid_" does not match system details or was in an incorrect format",reject("Detail")="PID^"_$get(^TMP("HL7",$job,cnt1,"PID",1))_"^7~103"
	// check sex
	if sex'=$$Sex^HL7Common3(^TMP("HL7",$job,cnt1,"PID",8),"C") set reject="Sex sent for Patient Id "_extid_" does not match system details",reject("Detail")="PID^"_$get(^TMP("HL7",$job,cnt1,"PID",1))_"^8~103" quit 1
	quit 0
	
	// log errors
Error	if $zerror["READ"!$zerror["WRITE" set WriteRead=1
	do AckNak^HL7AckNak("AR","TrakCare Cache Error","",207,"Cache Error: "_$zerror)
	do BACK^%ETN set $ztrap="Error^"_$zname
	set SQLCODE=100
	quit



